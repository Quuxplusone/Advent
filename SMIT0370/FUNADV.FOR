***        ******  *      *** ****** ***      * ******* ***   * ******  ******
****       ***   *  *     *** ***    ****     *   ***   ***   * ***   * *** 
*** *      ***   *   *    *** ***    *** *    *   ***   ***   * ***   * *** 
***  *     ***   *    *   *** ***    ***  *   *   ***   ***   * ***   * *** 
*******    ***   *     *  *** ****** ***   *  *   ***   ***   * ******  ******
***    *   ***   *      * *** ***    ***    * *   ***   ***   * *** *   *** 
***     *  ***   *       **** ***    ***     **   ***   ***   * ***  *  *** 
***      * ******         *** ****** ***      *   ***    *****  ***   * ******
  
  
      PROGRAM FunAdv

***** FunAdv for VAX/VMS.
*     C. T. Smith, Jr.    89/02/24.
*
*     This version of Adventure has passed through many hand, including
*     Gary M. Palter (MIT), Charles B. Fulghum (GIT), John West (GIT),
*     and most recently Neal White III, who added quite a few differences
*     between the original program and this new version.
*
*     Computer lore has it that adventure was originally developed at MIT
*     under MULTICS.  A version was translated to fortran, which since has
*     since spread throughout the computer world, from mainframes to PCs. 
*     Many years ago, a copy made its way to Georgia Tech, and was hacked 
*     to run on the CDC Cyber systems installed there about 1975.
*
*     The sources came into Neal's hands some years later.  He changed the
*     code a bit, changed some features, added others, modified the database
*     format slightly...still, the basic design of the program was not changed
*     very much.  The original source was written back in the good old days
*     before fortran had things like a character type and strings, and all
*     text was stored as integers.  It was clear from the source that at
*     one point, the program had been implemented on a machine that packed
*     five characers to a word.  The cyber systems packed 10 characters to
*     the word - sixbit characters.  Both of these implementations still
*     were clearly present in the source I started from.
*
*     As of this writing, there is still no conversiosn guide, and again,
*     the programs have seen quite a few changes while being moved to the Vax.
*     In particular, the game text is no longer stored in an integer 
*     array, though the data structure holding the text still has 
*     similarities to the integer implementation.  Also, when we deal
*     with alpha data, the fortran CHARACTER type is used, rather than
*     integer.  These changes should make it easier to transport the 
*     program, as the major hassle in doing this conversion were removing
*     all the places where so many characters could be packed in an 
*     integer.  Due to it's cyber heritage, words can be up to 10
*     characters long.  From the source I started with, its also apparent
*     that this program passed through a machine with 5 characters 
*     per integer, but much of this has been smoothed out in moving things
*     to a character data type.
*
*     Also, along the way, the text in the data base and program was
*     changed to mixed case from upper case only. 
*
*     Questions, problems, and bug reports should be sent to:
*
*         Charles T. Smith, Jr.
*
*         Internet:    cts@dragon.com
*         U.S. Mail:   2710 Regal Way  \  Tucker, GA  30084  \  USA
*
*     And, if you happen to have a copy of the conversion guide, or 
*     other versions of adventure, especally versions with greater than
*     500 points, I'd love to have a copy for my collection.
*
*     Enjoy!
*

***   Original Author info.
*
*     FURTHER INFORMATION WILL BE FOUND IN THE CONVERSION GUIDE ACCOMPANYING 
*     THIS PROGRAM.  FOR STILL FURTHER INFORMATION, CONTACT: 
*         GARY M. PALTER, MIT    (617) 253-7728 
*                (PALTER@MIT-MULTICS) 
* 
*     SINCE THERE IS NO CONVERSION GUIDE, AND I HAVE CHANGED THIS PROGRAM
*     IN MANY OBSCURE WAYS. TRY CONTACTING:  
*         NEAL WHITE III, GIT    (404) 436-1789 
*         P. O. BOX 31011 
*         GEORGIA TECH
*         ATLANTA, GEORGIA 30332
  


**    Things to do:
*
*      1)  Disable control C/Y.
*



***   ADVENTURE
* 
*     CURRENT LIMITS:  
*
*        2000 LINES OF MESSAGE TEXT (LINES, LINSIZ).
*        1500 TRAVEL OPTIONS (TRAVEL, TRVSIZ) 
*         400 VOCABULARY WORDS (KTAB, ATAB, TABSIZ).
*         220 LOCATIONS (LTEXT, STEXT, KEY, COND, ABB, ATLOC, LOCSIZ).
*         100 OBJECTS (PLAC, PLACE, FIXD, FIXED, LINK (TWICE), PTEXT, PROP).
*          35 'ACTION' VERBS (ACTSPK, VRBSIZ).
*         250 RANDOM MESSAGES (RTEXT, RTXSIZ).
*          12 DIFFERENT PLAYER CLASSIFICATIONS (CTEXT, CVAL, CLSMAX). 
*          20 HINTS, LESS 3 (HINTLC, HINTED, HINTS, HNTSIZ).
*           2 OBJECTS WHICH MAY BE WORN (WEARING).
*
*     THERE ARE ALSO LIMITS WHICH CANNOT BE EXCEEDED DUE TO THE STRUCTURE OF 
*     THE DATABASE.  (E.G., THE VOCABULARY USES N/1000 TO DETERMINE WORD TYPE, 
*     SO THERE CAN'T BE MORE THAN 1000 WORDS.)  THESE UPPER LIMITS ARE:  
*
*        1000 NON-SYNONYMOUS VOCABULARY WORDS 
*         300 LOCATIONS 
*         100 OBJECTS 
*

  
      IMPLICIT INTEGER(A-Z) 

      STRUCTURE /TEXTYPE/
        Integer*4 count
        Character*80 string
      end structure

      LOGICAL DSEEN,BLKLIN,HINTED,YES 
      LOGICAL WIZARD
      LOGICAL TOTING,HERE,AT,BITSET,DARK,WZDARK,LMWARN,CLOSNG,PANIC,
     $CLOSED,GAVEUP,SCORNG,FORCED,PCT 
      LOGICAL WIZKID,LOOSE,ROW,LIT,BFULL,ONCE,WEARING(2),YESCALL

      record /textype/ lines 

      Character*80 text, incheck  
      Character*10 Atab
      Character*10 WD1, WD2
      Character*128 filedir
      Real Secnds

**    Note.
*
*     All information to be "saved" between games must be in common
*     blocks and between /AAAAAA/ and /ZZZZZZ/!
*

      COMMON /AAAAAA/ CORE(1) 
      COMMON /TXTCOM/ RTEXT,LINES,LINUSE
      COMMON /VOCCOM/ TABSIZ,KTAB,ATAB
      COMMON /PLACOM/ ATLOC,LINK,PLACE,FIXED,COND,PROP,LOC,LAMP,HOLDNG
      COMMON /PTXCOM/ PTEXT 
      COMMON /STXCOM/ TEXT
      COMMON /IOSCOM/ BLKLIN,YESCALL
      COMMON /VARCOM/ WIZKID,ABB,ABBNUM,ACTSPK,ATTACK,AXE,BACK,BATTER,
     $BEAR,BFULL,BOAT,BOATPOS,BOOK,BOTTLE,CAVE,CHAIN,CHAIR,
     $CHASM,CHEST,CHLOC,CHLOC2,CLAM,
     $CLOCK1,CLOCK2,CLOSED,CLOSNG,CLSMAX,CLSSES,COAL,COINS,CTEXT,CVAL,
     $DALTLC,DETAIL,DFLAG,DKILL,DLOC,DOOR,
     $DPRSSN,DRAGON,DSEEN,DTOTAL,DWARF,
     $EGGS,EMRALD,ENTRNC,FIND,FISSUR,FIXD,FOOBAR,FOOD,FOUNTN,GAVEUP,
     $GO2,GRATE,HINT,HINTED,HINTLC,HINTM3,HINTS,HLDMAX,HNTMAX,HNTSIZ, 
     $INVENT,IWEST,KEY,KEYS,KNFLOC,KNIFE,K1,K2,
     $LIMIT,LISTING,LIT,LL,LMWARN,LOCK,
     $LOCSIZ,LOOK,LOOSE,LTEXT,MAGZIN,MAIL,MAXTRS,MESSAG,MIRROR,MISTS,
     $MONGOOS,NEWLOC,NUGGET,NULL,NUMDIE,MAXDIE,ODLOC,OFF,OIL,OLDLC2,
     $OLDLOC,ON,ONCE,OOPS,OYSTER,PANIC,PEARL,PILLOW,PLAC,PLANT,PLANT2,
     $POTION,PYRAM,RING,RINGLOC,RIVLOC,
     $ROD,ROD2,ROPE,ROW,RTXSIZ,RUG,RUNES,SACK,
     $SAVED,SAY,SCORE,SCORNG,SETUP,SHIT,sit,SNAKE,SPICES,SPK,STEPS,STEXT, 
     $STICK,SUSPND,SWITCH,TABLET,TABNDX,TALLY,TALLY2,THROW,TRAP,TRAVEL,
     $TRIDNT,TROLL,TROLL2,TRSURE,TRVS,TRVSIZ,TURNS,VASE,VEND,VRBSIZ,
     $WAITNG,WATER,WEARING,WHERE,WZDARK,XXM,YYM

      common /seed/ seed
      COMMON /USER/   USERNUM 
      COMMON /ZZZZZZ/ THEEND

      DIMENSION LINES(2000), TRAVEL(1500), RIVLOC(8) 
      DIMENSION KTAB(400), ATAB(400)
      DIMENSION LTEXT(220), STEXT(220), KEY(220), COND(220), ABB(220),
     $ATLOC(220)
      DIMENSION PLAC(100),PLACE(100),FIXD(100),FIXED(100),LINK(200),
     $PTEXT(100),PROP(100)
      DIMENSION ACTSPK(35), RTEXT(250)
      DIMENSION CTEXT(12),CVAL(12)
      DIMENSION HINTLC(20),HINTED(20),HINTS(20,4) 
      DIMENSION TK(20),DSEEN(8),DLOC(8),ODLOC(8)
  
      DATA LINSIZ/2000/, TRVSIZ/1500/, LOCSIZ/220/, VRBSIZ/35/,
     $RTXSIZ/250/, CLSMAX/12/, HNTSIZ/20/ 

  
**    STATEMENT FUNCTIONS
* 
*     TOTING(OBJ)  = TRUE IF THE OBJ IS BEING CARRIED
*     HERE(OBJ)    = TRUE IF THE OBJ IS AT 'LOC' (OR IS BEING CARRIED) 
*     AT(OBJ)      = TRUE IF ON EITHER SIDE OF TWO-PLACED OBJECT 
*     LIQ(DUMMY)   = OBJECT NUMBER OF LIQUID IN BOTTLE 
*     LIQLOC(LOC)  = OBJECT NUMBER OF LIQUID (IF ANY) AT LOC 
*     BITSET(L,N)  = TRUE IF COND(L) HAS BIT N SET (BIT 0 IS UNITS BIT)
*     FORCED(LOC)  = TRUE IF LOC MOVES WITHOUT ASKING FOR INPUT (COND=2) 
*     DARK(DUMMY)  = TRUE IF LOCATION 'LOC' IS DARK
*     PCT(N)       = TRUE N% OF THE TIME (N INTEGER FROM 0 TO 100) 
  
      TOTING(OBJ)=PLACE(OBJ).EQ.-1
      HERE(OBJ)=PLACE(OBJ).EQ.LOC.OR.PLACE(OBJ).LT.0
      AT(OBJ)=PLACE(OBJ).EQ.LOC.OR.FIXED(OBJ).EQ.LOC
      LIQ2(PBOTL)=(1-PBOTL)*WATER+(PBOTL/2)*(WATER+OIL) 
      LIQ(DUMMY)=LIQ2(MAX0(PROP(BOTTLE),-1-PROP(BOTTLE))) 
      LIQLOC(LOC)=LIQ2((MOD(COND(LOC)/2*2,8)-5)*MOD(COND(LOC)/4,2)+1) 
      BITSET(L,N)=(COND(L).AND.SHIFT(1,N)).NE.0
      FORCED(LOC)=COND(LOC).EQ.2
      DARK(DUMMY)=MOD(COND(LOC),2).EQ.0.AND.(PROP(LAMP).EQ.0.OR.
     $.NOT.HERE(LAMP)).AND. .NOT. LIT 
      PCT(N)=random(100).LT.N

  
**    LOAD 'SYSTEM' COMMON BLOCKS.  THESE COMMON BLOCKS DEFINE THE STATE 
*     OF A GAME WHICH HAS YET TO BE STARTED... 
* 
*     NOTE: SETUP=0 MEANS READ IN THE DATABASE.
* 
* 
      SETUP=0 
      CALL LDCOMN(.TRUE.,FLNAME)
  
*     READ IN THE DATABASE IF WE HAVE NOT YET DONE SO. 
  
      IF(SETUP.NE.0) GOTO 1100

  
***   DESCRIPTION OF THE DATABASE FORMAT 
* 
* 
*     THE DATA FILE CONTAINS SEVERAL SECTIONS. EACH BEGINS WITH A LINE CONTAINING
*     A NUMBER IDENTIFYING THE SECTION, AND ENDS WITH A LINE CONTAINING '-1'.
* 
*
*
*     SECTION 1: LONG FORM DESCRIPTIONS.  EACH LINE CONTAINS A LOCATION NUMBER,
*        A TAB, AND A LINE OF TEXT.  THE SET OF (NECESSARILY ADJACENT) LINES.
*        WHOSE NUMBERS ARE X FORM THE LONG DESCRIPTION OF LOCATION X.
*
*
*     SECTION 2: SHORT FORM DESCRIPTIONS.  SAME FORMAT AS LONG FORM.  NOT ALL
*        PLACES HAVE SHORT DESCRIPTIONS. 
*
*
*     SECTION 3: TRAVEL TABLE. EACH LINE CONTAINS A LOCATION NUMBER (X), A SECOND
*        LOCATION NUMBER (Y), AND A LIST OF MOTION NUMBERS (SEE SECTION 4).
*        EACH MOTION REPRESENTS A VERB WHICH WILL GO TO Y IF CURRENTLY AT X. 
*        Y, IN TURN, IS INTERPRETED AS FOLLOWS.  LET M=Y/1000, N=Y MOD 1000. 
*
*               IF N<=300       IT IS THE LOCATION TO GO TO.
*               IF 300<N<=500   N-300 IS USED IN A COMPUTED GOTO TO 
*                                       A SECTION OF SPECIAL CODE.
*               IF N>500        MESSAGE N-500 FROM SECTION 6 IS PRINTED,
*                                       AND HE STAYS WHEREVER HE IS.
*
*        MEANWHILE, M SPECIFIES THE CONDITIONS ON THE MOTION.
*
*               IF M=0          IT'S UNCONDITIONAL. 
*               IF 0<M<100      IT IS DONE WITH M% PROBABILITY. 
*               IF M=100        UNCONDITIONAL, BUT FORBIDDEN TO DWARVES.
*               IF 100<M<=200   HE MUST BE CARRYING OBJECT M-100. 
*               IF 200<M<=300   MUST BE CARRYING OR IN SAME ROOM AS M-200.
*               IF 300<M<=400   PROP(M MOD 100) MUST *NOT* BE 0.
*               IF 400<M<=500   PROP(M MOD 100) MUST *NOT* BE 1.
*               IF 500<M<=600   PROP(M MOD 100) MUST *NOT* BE 2, ETC. 
*
*        IF THE CONDITION (IF ANY) IS NOT MET, THEN THE NEXT *DIFFERENT* 
*        'DESTINATION' VALUE IS USED (UNLESS IT FAILS TO MEET *ITS* CONDITIONS,
*        IN WHICH CASE THE NEXT IS FOUND, ETC.).  TYPICALLY, THE NEXT DEST WILL
*        BE FOR ONE OF THE SAME VERBS, SO THAT ITS ONLY USE IS AS THE ALTERNATE
*        DESTINATION FOR THOSE VERBS.  FOR INSTANCE: 
*
*               15  110022      29      31      34      35      23
*               15      14      29
*
*        THIS SAYS THAT, FROM LOC 15, ANY OF THE VERBS 29, 31, ETC., WILL TAKE 
*        HIM TO 22 IF HE'S CARRYING OBJECT 10, AND OTHERWISE WILL GO TO 14.
*
*               11  303008      49
*               11       9      50
*
*        THIS SAYS THAT, FROM 11, 49 TAKES HIM TO 8 UNLESS PROP(3)=0, IN WHICH 
*        CASE HE GOES TO 9.  VERB 50 TAKES HIM TO 9 REGARDLESS OF PROP(3). 
*
*
*     SECTION 4: VOCABULARY.  EACH LINE CONTAINS A NUMBER (N), A TAB, AND A
*        FIVE-LETTER WORD.  CALL M=N/1000.  IF M=0, THEN THE WORD IS A MOTION
*        VERB FOR USE IN TRAVELLING (SEE SECTION 3).  ELSE, IF M=1, THE WORD IS
*        AN OBJECT.  ELSE, IF M=2, THE WORD IS AN ACTION VERB (SUCH AS 'CARRY' 
*        OR 'ATTACK').  ELSE, IF M=3, THE WORD IS A SPECIAL CASE VERB (SUCH AS 
*        'DIG') AND N MOD 1000 IS AN INDEX INTO SECTION 6.  OBJECTS FROM 50 TO 
*        (CURRENTLY, ANYWAY) 79 ARE CONSIDERED TREASURES(FOR PIRATE, CLOSEOUT).
*        IF M=1 (OBJECT) THERE IS AN OBJECT LOCATION TABLE ATTACHED.  EACH LINE
*        CONTAINS AN OBJECT'S INITIAL LOCATION (ZERO IF NONE).  IF AN OBJECT IS
*        IMMOVABLE, THE LOCATION IS FOLLOWED BY A '-1'.  IF THE OBJECT HAS TWO 
*        LOCATIONS IT IS FOLLOWED BY THE SECOND LOCATION; IT IS ASSUMED TO BE
*        IMMOVABLE.
*
*
*     SECTION 5: OBJECT DESCRIPTIONS.  EACH LINE CONTAINS A NUMBER (N), A TAB, 
*        AND A MESSAGE.  IF N IS FROM 1 TO 100, THE MESSAGE IS THE 'INVENTORY' 
*        MESSAGE FOR OBJECT N.  OTHERWISE, N SHOULD BE 000, 100, 200, ETC., AND
*        THE MESSAGE SHOULD BE THE DESCRIPTION OF THE PRECEDING OBJECT WHEN ITS
*        PROP VALUE IS N/100.  THE N/100 IS USED ONLY TO DISTINGUISH MULTIPLE
*        MESSAGES FROM MULTI-LINE MESSAGES; THE PROP INFO ACTUALLY REQUIRES ALL
*        ME SSAGES FOR AN OBJECT TO BE PRESENT AND CONSECUTIVE. PROPERTIES WHICH
*        PRODUCE NO MESSAGE SHOULD BE GIVEN THE MESSAGE '>$<'. 
*
*
*     SECTION 6: ARBITRARY MESSAGES.  SAME FORMAT AS SECTIONS 1, 2, AND 5, 
*        EXCEPTTHE NUMBERS BEAR NO RELATION TO ANYTHING (EXCEPT FOR SPECIAL 
*        VERBS IN SECTION 4).
*
*
*     SECTION 7: ACTION DEFAULTS.  EACH LINE CONTAINS AN 'ACTION-VERB' NUMBER
*       AND THE INDEX (IN SECTION 6) OF THE DEFAULT MESSAGE FOR THE VERB. 
*
*
*     SECTION 8: LIQUID ASSETS, ETC. EACH LINE CONTAINS A NUMBER (N) AND UP TO
*        20 LOCATION NUMBERS. BIT N (WHERE 0 IS THE UNITS BIT) IS SET IN 
*        COND(LOC) FOR EACH LOC GIVEN.  THE COND BITS CURRENTLY ASSIGNED ARE:  
*
*               0       LIGHT 
*               1       IF BIT 2 IS ON: ON FOR OIL, OFF FOR WATER 
*               2       LIQUID ASSET, SEE BIT 1 
*               3       PIRATE DOESN'T GO HERE UNLESS FOLLOWING PLAYER
*
*        OTHER BITS ARE USED TO INDICATE AREAS OF INTEREST TO 'HINT' ROUTINES: 
*
*               4       TRYING TO GET INTO CAVE 
*               5       TRYING TO CATCH MONGOOS 
*               6       TRYING TO DEAL WITH SNAKE 
*               7       LOST IN MAZE
*               8       PONDERING DARK ROOM 
*               9       AT WITT'S END 
*
*        COND(LOC) IS SET TO 2, OVERRIDING ALL OTHER BITS, IF LOC HAS FORCED 
*        MOTION. 
*
*
*     SECTION 9: CLASS MESSAGES.  EACH LINE CONTAINS A NUMBER (N), A TAB, AND A
*        MESSAGE DESCRIBING A CLASSIFICATION OF PLAYER.  THE SCORING SECTION 
*        SELECTS THE APPROPRIATE MESSAGE, WHERE EACH MESSAGE IS CONSIDERED TO
*        APPLY TO PLAYERS WHOSE SCORES ARE HIGHER THAN THE PREVIOUS N BUT NOT
*        HIGHER THAN THIS N.  NOTE THAT THESE SCORES PROBABLY CHANGE WITH EVERY
*        MODIFICATION (AND PARTICULARLY EXPANSION) OF THE PROGRAM. 
*    SECTION 10: HINTS.  EACH LINE CONTAINS A HINT NUMBER (CORRESPONDING TO A 
*        COND BIT, SEE SECTION 9), THE NUMBER OF TURNS HE MUST BE AT THE RIGHT 
*        LOC(S) BEFORE TRIGGERING THE HINT, THE POINTS DEDUCTED FOR TAKING THE 
*        HINT, THE MESSAGE NUMBER (SECTION 6) OF THE QUESTION, AND THE MESSAGE 
*        NUMBER OF THE HINT.  THESE VALUES ARE STASHED IN THE 'HINTS' ARRAY. 
*        HNTMAX IS SET TO THE MAX HINT NUMBER (<= HNTSIZ).  NUMBERS 1-3 ARE
*        UNUSABLE SINCE COND BITS ARE OTHERWISE ASSIGNED, SO 2 IS USED TO
*        REMEMBER IF HE'S READ THE CLUE IN THE REPOSITORY, AND 3 IS USED TO
*        REMEMBER WHETHER HE ASKED FOR INSTRUCTIONS (GETS MORE TURNS, BUT LOSES
*        POINTS).
*
*
*     SECTION 0: END OF DATABASE.
* 


      WRITE(6,1000) 
 1000 FORMAT(/,' Initializing...')
  
***CLEAR OUT THE VARIOUS TEXT-POINTER ARRAYS.  ALL TEXT IS STORED IN ARRAY
*  LINES; EACH LINE IS PRECEDED BY A WORD POINTING TO THE NEXT POINTER (I.E.
*  THE WORD FOLLOWING THE END OF THE LINE). THE POINTER IS NEGATIVE IF THIS IS
*  FIRST LINE OF A MESSAGE.  THE TEXT-POINTER ARRAYS CONTAIN INDICES OF 
*  POINTER-WORDS IN LINES.  STEXT(N) IS SHORT DESCRIPTION OF LOCATION N.
*  LTEXT(N) IS LONG DESCRIPTION.  PTEXT(N) POINTS TO MESSAGE FOR PROP(N)=0. 
*  SUCCESSIVE PROP MESSAGES ARE FOUND BY CHASING POINTERS.  RTEXT CONTAINS
*  SECTION 6'S STUFF.CTEXT(N) POINTS TO A PLAYER-CLASS MESSAGE.  MTEXT IS FOR 
*  SECTION 12.  WE ALSO CLEAR COND.  SEE DESCRIPTION OF SECTION 9 FOR DETAILS 

      call image_dir (filedir)

      open (Unit=1, err=201,
     +      file=filedir//'funadv.dat',
     +      carriagecontrol='none', status='old')
      goto 300
  201 WRITE(6,200)
  200 FORMAT(' Data base not found.'/' See program maintainer.')
  300 LIT=.FALSE. 
      BLKLIN=.FALSE.
  
      TABSIZ=400
      SETUP=1 
      LINUSE=1
      TRVS=1
      CLSSES=1
  
***START NEW DATA SECTION.  SECT IS THE SECTION NUMBER. 
  
 1002 READ(1,1003)SECT
 1003 FORMAT(I8)
      OLDLOC=-1 
      SECT1=SECT+1
      IF(SECT1.LT.1.OR.SECT1.GT.11) CALL BUG(9) 
      GOTO(1100,1004,1004,1030,1040,1004,1004,1060,1070,1004,1080),SECT1
*           (0)  (1)  (2)  (3)  (4)  (5)  (6)  (7)  (8)  (9)  (10)
  
*  SECTIONS 1, 2, 5, 6, 9.  READ MESSAGES AND SET UP POINTERS.
  
 1004 READ(1,1005)LOC,TEXT,INCHECK
 1005 FORMAT(1I8,80A,80A)
      IF(INCHECK.NE.' ') WRITE(6,310) LOC,TEXT
  310 FORMAT(' *Line is over 80 char. long* CALL BUG(0)',/,I8,80A) 
      IF(LOC.EQ.-1)GOTO 1002
      DO 1006 LOOP=1,80
      KK=81-LOOP 
      IF (TEXT(KK:KK).NE.' ')GOTO 1007
 1006 CONTINUE
      CALL BUG(1) 
 1007 Lines(LINUSE).STRING = text

 1008 CONTINUE
      KK=LINUSE+KK
      LINES(LINUSE).COUNT=linuse+1
      IF(LOC.EQ.OLDLOC)GOTO 1020
      LINES(LINUSE).COUNT=-linuse-1
      IF(SECT.EQ.9)GOTO 1012
      IF(SECT.EQ.6)GOTO 1011
      IF(SECT.EQ.5)GOTO 1010
      IF(SECT.EQ.1)GOTO 1009
  
      STEXT(LOC)=LINUSE 
      GOTO 1020 
  
 1009 LTEXT(LOC)=LINUSE 
      GOTO 1020 
  
 1010 IF(LOC.GT.0.AND.LOC.LE.100)PTEXT(LOC)=LINUSE
      GOTO 1020 
  
 1011 IF(LOC.GT.RTXSIZ)CALL BUG(6)
      RTEXT(LOC)=LINUSE 
      GOTO 1020 
  
 1012 CTEXT(CLSSES)=LINUSE
      CVAL(CLSSES)=LOC
      CLSSES=CLSSES+1 
  
 1020 LINUSE=LINUSE+1 
      LINES(LINUSE).COUNT=-1
      OLDLOC=LOC
      IF(LINUSE+1.GT.LINSIZ)CALL BUG(2)
      GOTO 1004 
  
***THE STUFF FOR SECTION 3 IS ENCODED HERE.  EACH 'FROM-LOCATION' GETS A
*  CONTIGUOUS SECTION OF THE 'TRAVEL' ARRAY.  EACH ENTRY IN TRAVEL IS 
*  NEWLOC*1000 + KEYWORD (FROM SECTION 4, MOTION VERBS), AND IS NEGATED IF
*  THIS IS THE LAST ENTRY FOR THIS LOCATION.  KEY(N) IS THE INDEX IN TRAVEL 
*  OF THE FIRST OPTION AT LOCATION N. 
  
 1030 READ(1,1031)LOC,NEWLOC,(TK(I),I=1,8)
 1031 FORMAT(10I8)
      IF(LOC.EQ.-1)GOTO 1002
      IF(KEY(LOC).NE.0)GOTO 1033
      KEY(LOC)=TRVS 
      GOTO 1035 
 1033 TRAVEL(TRVS-1)=-TRAVEL(TRVS-1)
 1035 DO 1037 LOOP=1,8
      IF(TK(LOOP).EQ.0)GOTO 1039
      TRAVEL(TRVS)=NEWLOC*1000+TK(LOOP) 
      TRVS=TRVS+1 
      IF(TRVS.EQ.TRVSIZ)CALL BUG(3) 
 1037 CONTINUE
 1039 TRAVEL(TRVS-1)=-TRAVEL(TRVS-1)
      GOTO 1030 
  
***HERE WE READ IN THE VOCABULARY.  KTAB(N) IS THE WORD NUMBER, ATAB(N) IS
*  THE CORRESPONDING WORD.  THE -1 AT THE END OF SECTION 4 IS LEFT IN KTAB
*  AS AN END-MARKER.  THE WORDS ARE GIVEN A MINIMAL HASH TO MAKE READING THE
*  CORE-IMAGE HARDER. 
  
 1040 DO 1042 LOOP=1,TABSIZ 
      TABNDX=LOOP 
      READ(1,1041)KTAB(TABNDX),TEXT(1:10), J, K
 1041 FORMAT(I8,1A10,2I8) 
      OBJ=KTAB(LOOP)
      IF(OBJ.EQ.-1)GOTO 1002
  
***READ IN THE INITIAL LOCATIONS FOR EACH OBJECT.  ALSO THE IMMOVABILITY INFO.
*  PLAC CONTAINS INITIAL LOCATIONS OF OBJECTS.  FIXD IS -1 FOR IMMOVABLE
*  OBJECTS (INCLUDING THE SNAKE), OR = SECOND LOC FOR TWO-PLACED OBJECTS. 
  
      IF((OBJ/1000) .NE. 1) GOTO 1042 
      OBJ=OBJ-1000
      PLAC(OBJ)=J 
      FIXD(OBJ)=K 
 1042 ATAB(TABNDX) = text(1:10)
      CALL BUG(4) 
  
*  READ DEFAULT MESSAGE NUMBERS FOR ACTION VERBS, STORE IN ACTSPK.
  
 1060 READ(1,1031)VERB,J
      IF(VERB.EQ.-1)GOTO 1002 
      ACTSPK(VERB)=J
      GOTO 1060 
  
*  READ INFO ABOUT AVAILABLE LIQUIDS AND OTHER CONDITIONS, STORE IN COND. 
  
 1070 READ(1,1031)K,(TK(I),I=1,9) 
      IF(K.EQ.-1)GOTO 1002
      DO 1071 LOOP=1,9
      LOC=TK(LOOP)
      IF(LOC.EQ.0)GOTO 1070 
      IF(BITSET(LOC,K))CALL BUG(8)
 1071 COND(LOC)=COND(LOC)+SHIFT(1,K)
      GOTO 1070 
  
*  READ DATA FOR HINTS. 
  
 1080 HNTMAX=0
 1081 READ(1,1031)K,(TK(I),I=1,8) 
      IF(K.EQ.-1)GOTO 1002
      IF(K.EQ.0)GOTO 1081 
      IF(K.LT.0.OR.K.GT.HNTSIZ)CALL BUG(7)
      DO 1083 LOOP=1,4
 1083 HINTS(K,LOOP)=TK(LOOP)
      HNTMAX=MAX0(HNTMAX,K) 
      GOTO 1081 
*** 
*  FINISH CONSTRUCTING INTERNAL DATA FORMAT 
* 
*  IF SETUP=2 WE DON'T NEED TO DO THIS.  IT'S ONLY NECESSARY IF WE HAVEN'T
*  DONE IT AT ALL OR IF THE PROGRAM HAS BEEN RUN SINCE THEN.
 1100 close (1)  
      IF(SETUP.EQ.2)GOTO 1
      IF(SETUP.NE.-1)GOTO 8305
      SETUP=3 
      K=NULL
      GOTO 2900 
  
***HAVING READ IN THE DATABASE, CERTAIN THINGS ARE NOW CONSTRUCTED.  PROPS ARE
*  SET TO ZERO.  WE FINISH SETTING UP COND BY CHECKING FOR FORCED-MOTION TRAVEL 
*  ENTRIES.  THE PLAC AND FIXD ARRAYS ARE USED TO SET UP ATLOC(N) AS THE FIRST
*  OBJECT AT LOCATION N, AND LINK(OBJ) AS THE NEXT OBJECT AT THE SAME LOCATION
*  AS OBJ.  (OBJ>100 INDICATES THAT FIXED(OBJ-100)=LOC; LINK(OBJ) IS STILL THE
*  CORRECT LINK TO USE.)  ABB IS ZEROED; IT CONTROLS WHETHER THE ABBREVIATED
*  DESCRIPTION IS PRINTED.  COUNTS MOD 5 UNLESS 'LOOK' IS USED. 
  
 8305 DO 1102 LOOP=1,LOCSIZ 
      IF(LTEXT(LOOP).EQ.0.OR.KEY(LOOP).EQ.0)GOTO 1102 
      K=KEY(LOOP) 
      IF(MOD(IABS(TRAVEL(K)),1000).EQ.1)COND(LOOP)=2
 1102 CONTINUE
  
***SET UP THE ATLOC AND LINK ARRAYS AS DESCRIBED ABOVE.  WE'LL USE THE DROP 
*  SUBROUTINE, WHICH PREFACES NEW OBJECTS ON THE LISTS.  SINCE WE WANT THINGS 
*  IN THE OTHER ORDER, WE'LL RUN THE LOOP BACKWARDS.  IF THE OBJECT IS IN TWO 
*  LOCS, WE DROP IT TWICE.  THIS ALSO SETS UP 'PLACE' AND 'FIXED' AS COPIES OF
*  'PLAC' AND 'FIXD'.  ALSO, SINCE TWO-PLACED OBJECTS ARE TYPICALLY BEST
*  DESCRIBED LAST, WE'LL DROP THEM FIRST. 
  
      DO 1106 LOOP=1,100
      K=101-LOOP
      IF(FIXD(K).LE.0)GOTO 1106 
      CALL DROP(K+100,FIXD(K))
      CALL DROP(K,PLAC(K))
 1106 CONTINUE
  
      DO 1107 LOOP=1,100
      K=101-LOOP
      FIXED(K)=FIXD(K)
      IF(PLAC(K).NE.0.AND.FIXD(K).LE.0)CALL DROP(K,PLAC(K)) 
 1107 CONTINUE
  
***CLEAR THE HINT STUFF.  HINTLC(I) IS HOW LONG HE'S BEEN AT LOC WITH COND BIT
*  I.  HINTED(I) IS TRUE IFF HINT I HAS BEEN USED.
  
      DO 1300 LOOP=1,HNTMAX 
      HINTED(LOOP)=.FALSE.
 1300 CONTINUE
  
*  DEFINE SOME HANDY MNEMONICS.  THESE CORRESPOND TO OBJECT NUMBERS.
  
      KEYS=   VOCAB('KEYS      ',1) 
      LAMP=   VOCAB('LAMP      ',1) 
      GRATE=  VOCAB('GRATE     ',1) 
      SACK=   VOCAB('SACK      ',1) 
      ROD=    VOCAB('ROD       ',1) 
      ROD2=   ROD+1 
      STEPS=  VOCAB('STEPS     ',1) 
      MONGOOS=VOCAB('MONGOOSE  ',1) 
      DOOR=   VOCAB('DOOR      ',1) 
      PILLOW= VOCAB('PILLOW    ',1) 
      SNAKE=  VOCAB('SNAKE     ',1) 
      FISSUR= VOCAB('FISSURE   ',1) 
      TABLET= VOCAB('TABLET    ',1) 
      CLAM=   VOCAB('CLAM      ',1) 
      OYSTER= VOCAB('OYSTER    ',1) 
      MAGZIN= VOCAB('MAGAZINES ',1) 
      PIRATE= VOCAB('PIRATE    ',1) 
      DWARF=  VOCAB('DWARF     ',1) 
      KNIFE=  VOCAB('KNIFE     ',1) 
      FOOD=   VOCAB('FOOD      ',1) 
      BOTTLE= VOCAB('BOTTLE    ',1) 
      WATER=  VOCAB('WATER     ',1) 
      OIL=    VOCAB('OIL       ',1) 
      PLANT=  VOCAB('PLANT     ',1) 
      PLANT2= PLANT+1 
      AXE=    VOCAB('AXE       ',1) 
      MIRROR= VOCAB('MIRROR    ',1) 
      DRAGON= VOCAB('DRAGON    ',1) 
      CHASM=  VOCAB('CHASM     ',1) 
      TROLL=  VOCAB('TROLL     ',1) 
      TROLL2= TROLL+1 
      BEAR=   VOCAB('BEAR      ',1) 
      MESSAG= VOCAB('MESSAGE   ',1) 
      VEND=   VOCAB('MACHINE   ',1) 
      BATTER= VOCAB('BATTERIES ',1) 
      RUNES=  VOCAB('RUNES     ',1) 
      ROPE=   VOCAB('ROPE      ',1) 
      COAL=   VOCAB('COAL      ',1) 
      BOOK=   VOCAB('BOOK      ',1) 
      FOUNTN= VOCAB('FOUNTAIN  ',1) 
      POTION= VOCAB('POTION    ',1) 
      SWITCH= VOCAB('SWITCH    ',1) 
      SHIT=   VOCAB('SHIT      ',1) 
      BOAT=   VOCAB('BOAT      ',1) 
      TRAP=   VOCAB('TRAP      ',1) 
      chair=  vocab('CHAIR     ',1)
  
*  OBJECTS FROM TRSURE THROUGH WHATEVER ARE TREASURES.  HERE ARE A FEW. 
  
      NUGGET= VOCAB('GOLD      ',1) 
      COINS=  VOCAB('COINS     ',1) 
      CHEST=  VOCAB('CHEST     ',1) 
      EGGS=   VOCAB('EGGS      ',1) 
      TRIDNT= VOCAB('TRIDENT   ',1) 
      VASE=   VOCAB('VASE      ',1) 
      EMRALD= VOCAB('EMERALD   ',1) 
      PYRAM=  VOCAB('PYRAMID   ',1) 
      PEARL=  VOCAB('PEARL     ',1) 
      RUG=    VOCAB('RUG       ',1) 
      CHAIN=  VOCAB('CHAIN     ',1) 
      SPICES= VOCAB('SPICES    ',1) 
      MAIL=   VOCAB('MAIL      ',1) 
      LISTING=VOCAB('LISTINGS  ',1) 
      RING=   VOCAB('RING      ',1) 
  
*  THESE ARE MOTION-VERB NUMBERS. 
  
      BACK=   VOCAB('BACK      ',0) 
      LOOK=   VOCAB('LOOK      ',0) 
      CAVE=   VOCAB('CAVE      ',0) 
      NULL=   VOCAB('NULL      ',0) 
      ENTRNC= VOCAB('ENTRANCE  ',0) 
      DPRSSN= VOCAB('DEPRE     ',0) 
      GO2=    VOCAB('GOTO      ',0) 
      SIT=    VOCAB('SIT       ',0) 
  
*  AND SOME ACTION VERBS. 
  
      SAY=    VOCAB('SAY       ',2) 
      LOCK=   VOCAB('LOCK      ',2) 
      THROW=  VOCAB('THROW     ',2) 
      FIND=   VOCAB('FIND      ',2) 
      INVENT= VOCAB('INVEN     ',2) 
      SUSPND= VOCAB('SUSPE     ',2) 
      ON=     VOCAB('ON        ',2) 
      OFF=    VOCAB('OFF       ',2) 
  
***TREASURES, AS NOTED EARLIER, ARE OBJECTS TRSURE THROUGH MAXTRS (CURRENTLY 79). 
*  THEIR PROPS ARE INITIALLY -1, AND ARE SET TO 0 THE FIRST TIME THEY ARE 
*  DESCRIBED.  TALLY KEEPS TRACK OF HOW MANY ARE NOT YET FOUND, SO WE KNOW
*  WHEN TO CLOSE THE CAVE.  TALLY2 COUNTS HOW MANY CAN NEVER BE FOUND (E.G. IF
*  LOST MONGOOS OR BRIDGE). 
  
      TRSURE=NUGGET 
      MAXTRS=RING 
      TALLY=0 
      TALLY2=0
      DO 1200 LOOP=TRSURE,MAXTRS
      IF(PTEXT(LOOP).NE.0)PROP(LOOP)=-1 
      TALLY=TALLY-PROP(LOOP)
 1200 CONTINUE
  
***PUT THE MONGOOSE IN THE SACK.
  
      CALL CARRY(MONGOOS,PLAC(MONGOOS)) 
      PROP(MONGOOS)=1 
      PROP(SACK)=1
      CALL DROP(MONGOOS,PLAC(SACK)) 
  
***HIDE THE RING. 
  
      RINGLOC=PLAC(RING)
      CALL MOVE(RING,0) 
      PLAC(RING)=0
  
***INITIALIZE THE DWARVES.  DLOC IS LOC OF DWARVES, HARD-WIRED IN.  ODLOC IS
*  PRIOR LOC OF EACH DWARF, INITIALLY GARBAGE.  DALTLC IS ALTERNATE INITIAL LOC 
*  FOR DWARF, IN CASE ONE OF THEM STARTS OUT ON TOP OF THE ADVENTURER.  (NO TWO 
*  OF THE 7 INITIAL LOCS ARE ADJACENT.)  DSEEN IS TRUE IF DWARF HAS SEEN HIM. 
*  DFLAG CONTROLS THE LEVEL OF ACTIVATION OF ALL THIS:  
*       0       NO DWARF STUFF YET (WAIT UNTIL REACHES HALL OF MISTS) 
*       1       REACHED HALL OF MISTS, BUT HASN'T MET FIRST DWARF 
*       2       MET FIRST DWARF, OTHERS START MOVING, NO KNIVES THROWN YET
*       3       A KNIFE HAS BEEN THROWN (FIRST SET ALWAYS MISSES) 
*       3+      DWARVES ARE MAD (INCREASES THEIR ACCURACY)
*  EIGHTH DWARF IS SPECIAL (THE PIRATE).  HE ALWAYS STARTS AT HIS CHEST'S 
*  EVENTUAL LOCATION INSIDE THE MAZE.  THIS LOC IS SAVED IN CHLOC FOR REF.
*  THE DEAD END IN THE OTHER MAZE HAS ITS LOC STORED IN CHLOC2. 
  
      CHLOC=PLAC(CHEST) 
      CALL MOVE(CHEST,0)
      PLAC(CHEST)=0 
      CHLOC2=PLAC(VEND) 
      DO 1700 LOOP=1,8
 1700 DSEEN(LOOP)=.FALSE. 
      DFLAG=0 
      MISTS = 24
      DLOC(1)=MISTS 
      DLOC(2)=31
      DLOC(3)=42
      DLOC(4)=50
      DLOC(5)=53
      DLOC(6)=65
      DLOC(7)=113 
      DLOC(8)=CHLOC 
      DALTLC=97 
  
***OTHER RANDOM FLAGS AND COUNTERS, AS FOLLOWS: 
*       TURNS   TALLIES HOW MANY COMMANDS HE'S GIVEN (IGNORES YES/NO) 
*       LIMIT   LIFETIME OF LAMP (NOT SET HERE) 
*       IWEST   HOW MANY TIMES HE'S SAID 'WEST' INSTEAD OF 'W'
*       KNFLOC  0 IF NO KNIFE HERE, LOC IF KNIFE HERE, -1 AFTER CAVEAT
*       DETAIL  HOW OFTEN WE'VE SAID 'NOT ALLOWED TO GIVE MORE DETAIL'
*       ABBNUM  HOW OFTEN WE SHOULD PRINT NON-ABBREVIATED DESCRIPTIONS
*       MAXDIE  NUMBER OF REINCARNATION MESSAGES AVAILABLE (UP TO 5)
*       NUMDIE  NUMBER OF TIMES KILLED SO FAR 
*       HOLDNG  NUMBER OF OBJECTS BEING CARRIED 
*       DKILL   NUMBER OF DWARVES KILLED (UNUSED IN SCORING, NEEDED FOR MSG)
*       FOOBAR  CURRENT PROGRESS IN SAYING 'FEE FIE FOE FOO'. 
*       BONUS   USED TO DETERMINE AMOUNT OF BONUS IF HE REACHES CLOSING 
*       CLOCK1  NUMBER OF TURNS FROM FINDING LAST TREASURE TILL CLOSING 
*       CLOCK2  NUMBER OF TURNS FROM FIRST WARNING TILL BLINDING FLASH
* 
*  WZDARK SAYS WHETHER THE LOC HE'S LEAVING WAS DARK
*  LMWARN SAYS WHETHER HE'S BEEN WARNED ABOUT LAMP GOING DIM
*  CLOSNG SAYS WHETHER ITS CLOSING TIME YET 
*  PANIC SAYS WHETHER HE'S FOUND OUT HE'S TRAPPED IN THE CAVE 
*  CLOSED SAYS WHETHER WE'RE ALL THE WAY CLOSED 
*  GAVEUP SAYS WHETHER HE EXITED VIA 'QUIT' 
*  SCORNG INDICATES TO THE SCORE ROUTINE WHETHER WE'RE DOING A 'SCORE' COMMAND
*  BFULL INDICATES WEITHER HIS BLADDER IS FULL. 
*  LOOSE TELLS IF THE MONGOOSE IS LOOSE.
  
      TURNS=0 
      IWEST=0 
      KNFLOC=0
      DETAIL=0
      ABBNUM=5
      WCOUNT=0
      MAXDIE=10 
      NUMDIE=0
      HOLDNG=0
      HLDMAX=7
      DKILL=0 
      FOOBAR=0
      BONUS=0 
      CLOCK1=40 
      CLOCK2=50 
      SAVED=0 
      WAITNG=9999999
      OOPS=9999999
      LMWARN=.FALSE.
      CLOSNG=.FALSE.
      PANIC=.FALSE. 
      CLOSED=.FALSE.
      GAVEUP=.FALSE.
      SCORNG=.FALSE.
      BFULL=.FALSE. 
      LOOSE=.FALSE. 
      ROW=.FALSE. 
      YESCALL=.FALSE. 
  
***IF SETUP=1, REPORT ON AMOUNT OF ARRAYS ACTUALLY USED, TO PERMIT REDUCTIONS.
  
      IF(SETUP.NE.1)GOTO 19990
      SETUP=2 
  
      DO 1998 LOOP=1,LOCSIZ 
      KK=LOCSIZ+1-LOOP
      IF(LTEXT(KK).NE.0)GOTO 1997 
 1998 CONTINUE
  
      OBJ=0 
 1997 DO 1996 LOOP=1,100
 1996 IF(PTEXT(LOOP).NE.0)OBJ=OBJ+1 
  
      DO 1995 LOOP=1,TABNDX 
 1995 IF(KTAB(LOOP)/1000.EQ.2)VERB=KTAB(LOOP)-2000
  
      DO 1994 LOOP=1,RTXSIZ 
      J=RTXSIZ+1-LOOP 
      IF(RTEXT(J).NE.0)GOTO 1991
 1994 CONTINUE
  
 1991 K=100 
      WRITE(6,1999)LINUSE,LINSIZ,TRVS,TRVSIZ,TABNDX,TABSIZ,KK 
     $,LOCSIZ,OBJ,K,VERB,VRBSIZ,J,RTXSIZ,CLSSES,CLSMAX
     $,HNTMAX,HNTSIZ
 1999 FORMAT (' Table space used:',/
     $' ',I6,' OF ',I6,' Lines of messages',/ 
     $' ',I6,' OF ',I6,' Travel options',/
     $' ',I6,' OF ',I6,' Vocabulary words',/
     $' ',I6,' OF ',I6,' Locations',/ 
     $' ',I6,' OF ',I6,' Objects',/ 
     $' ',I6,' OF ',I6,' Action verbs',/
     $' ',I6,' OF ',I6,' Rtext messages',/
     $' ',I6,' OF ',I6,' Class messages',/
     $' ',I6,' OF ',I6,' Hints')
  
***FINALLY, SINCE WE'RE CLEARLY SETTING THINGS UP FOR THE FIRST TIME... 
  
19990 CONTINUE
  
**
*      MAKE SURE HE DOESN'T TERMINATE ACCIDENTALLY. 
* 
      WRITE(6,19991)
19991 FORMAT(' Initialization completed.')
  
***START-UP 
* 
    1 seed = ifix (secnds (0.0))
!CALL RANSET(SECOND(I))
      I=random(1)
      WIZKID=WIZARD(0)
      IF(.NOT.WIZKID) HINTED(3)=YES(65,1,0,WCOUNT)
      NEWLOC=1
      SETUP=3 
      LIMIT=400 
      IF(HINTED(3))LIMIT=1000 
  
***CAN'T LEAVE CAVE ONCE IT'S CLOSING (EXCEPT BY MAIN OFFICE).
  
    2 IF(NEWLOC.GE.13.OR.NEWLOC.EQ.0.OR..NOT.CLOSNG)GOTO 71 
      CALL RSPEAK(130)
      NEWLOC=LOC
      IF(.NOT.PANIC)CLOCK2=15 
      PANIC=.TRUE.
  
***                      *******************
*                        ***** DWARVES *****
*                        *******************
* 
*  SEE IF A DWARF HAS SEEN HIM AND HAS COME FROM WHERE HE WANTS TO GO.  IF SO,
*  THE DWARF'S BLOCKING HIS WAY.  IF COMING FROM PLACE FORBIDDEN TO PIRATE
*  (DWARVES ROOTED IN PLACE) LET HIM GET OUT (AND ATTACKED).
  
   71 IF(NEWLOC.EQ.LOC.OR.FORCED(LOC).OR.BITSET(LOC,3))GOTO 74
      DO 73 LOOP=1,7
      IF(ODLOC(LOOP).NE.NEWLOC.OR..NOT.DSEEN(LOOP))GOTO 73
      NEWLOC=LOC
      CALL RSPEAK(2)
      GOTO 74 
   73 CONTINUE
   74 LOC=NEWLOC
  
***DWARF STUFF.  SEE EARLIER COMMENTS FOR DESCRIPTION OF VARIABLES.  REMEMBER 
*  EIGHTH DWARF IS PIRATE AND IS THUS VERY DIFFERENT EXCEPT FOR MOTION RUN. 
* 
*  FIRST OFF, DON'T LET THE DWARVES FOLLOW HIM INTO A PIT OR A WALL.  ACTIVATE
*  THE WHOLE MESS THE FIRST TIME HE GETS AS FAR AS THE HALL OF MISTS. 
*  IF NEWLOC IS FORBIDDEN TO PIRATE (IN PARTICULAR, IF IT'S BEYOND THE TROLL
*  BRIDGE), BYPASS DWARF STUFF.  THAT WAY PIRATE CAN'T STEAL RETURN TOLL AND
*  DWARVES CAN'T MEET THE BEAR.  ALSO MEANS DWARVES WON'T FOLLOW HIM INTO DEAD
*  END IN MAZE, BUT C'EST LA VIE.  THEY'LL WAIT FOR HIM OUTSIDE THE DEAD END. 
  
      IF(LOC.EQ.0.OR.FORCED(LOC).OR.BITSET(NEWLOC,3))GOTO 2000
      IF(DFLAG.NE.0)GOTO 6000 
      IF(LOC.GE.MISTS)DFLAG=1 
      GOTO 2000 
  
***WHEN WE ENCOUNTER THE FIRST DWARF, WE KILL 0, 1, OR 2 OF THE 5 DWARVES. IF 
*  ANY OF THE SURVIVORS IS AT LOC, REPLACE HIM WITH THE ALTERNATE.
  
 6000 IF(DFLAG.NE.1)GOTO 6010 
      IF(LOC.LT.MISTS.OR.PCT(95))GOTO 2000
      DFLAG=2 
      DO 6001 LOOP=1,2
      J=1+random(5)
  
*  IF SAVED NOT = -1, HE BYPASSED THE 'START' CALL. 
  
      IF(PCT(50).AND.SAVED.EQ.-1)DLOC(J)=0
 6001 CONTINUE
      DO 6002 LOOP=1,7
      IF(DLOC(LOOP).EQ.LOC)DLOC(LOOP)=DALTLC
 6002 ODLOC(LOOP)=DLOC(LOOP)
      CALL RSPEAK(3)
      CALL DROP(AXE,LOC)
      GOTO 2000 
  
***THINGS ARE IN FULL SWING.  MOVE EACH DWARF AT RANDOM, EXCEPT IF HE'S BEEN SEEN 
*  HE STICKS WITH US.  DWARVES NEVER GO TO LOCS <MISTS.  IF WANDERING AT RANDOM 
*  THEY DON'T BACK UP UNLESS THERE'S NO ALTERNATIVE.  IF THEY DON'T HAVE TO 
*  MOVE, THEY ATTACK.  AND, OF COURSE, DEAD DWARVES DON'T DO MUCH OF ANYTHING.
  
 6010 DTOTAL=0
      ATTACK=0
      STICK=0 
      DO 6030 LOOP=1,8
      IF(DLOC(LOOP).EQ.0)GOTO 6030
      J=1 
      KK=DLOC(LOOP) 
      KK=KEY(KK)
      IF(KK.EQ.0)GOTO 6016
 6012 NEWLOC=MOD(IABS(TRAVEL(KK))/1000,1000)
      IF(NEWLOC.GT.300.OR.NEWLOC.LT.MISTS.OR.NEWLOC.EQ.ODLOC(LOOP)
     $.OR.(J.GT.1.AND.NEWLOC.EQ.TK(J-1)).OR.J.GE.20 
     $.OR.NEWLOC.EQ.DLOC(LOOP).OR.FORCED(NEWLOC)
     $.OR.(LOOP.EQ.8.AND.BITSET(NEWLOC,3))
     $.OR.IABS(TRAVEL(KK))/1000000.EQ.100)GOTO 6014 
      TK(J)=NEWLOC
      J=J+1 
 6014 KK=KK+1 
      IF(TRAVEL(KK-1).GE.0)GOTO 6012
 6016 TK(J)=ODLOC(LOOP) 
      IF(J.GE.2)J=J-1 
      J=1+random(J)
      ODLOC(LOOP)=DLOC(LOOP)
      DLOC(LOOP)=TK(J)
      DSEEN(LOOP)=(DSEEN(LOOP).AND.LOC.GE.MISTS)
     $.OR.(DLOC(LOOP).EQ.LOC.OR.ODLOC(LOOP).EQ.LOC) 
      IF(.NOT.DSEEN(LOOP))GOTO 6030 
      DLOC(LOOP)=LOC
      IF(LOOP.NE.8)GOTO 6027
  
***THE PIRATE'S SPOTTED HIM.  HE LEAVES HIM ALONE ONCE WE'VE FOUND CHEST. 
*  K COUNTS IF A TREASURE IS HERE.  IF NOT, AND TALLY=TALLY2 PLUS ONE FOR 
*  AN UNSEEN CHEST, LET THE PIRATE BE SPOTTED.
  
      IF(LOC.EQ.CHLOC.OR.PROP(CHEST).GE.0)GOTO 6030 
      K=0 
      DO 6020 LOOP2=TRSURE,MAXTRS 
  
***PIRATE WON'T TAKE PYRAMID FROM PLOVER ROOM OR DARK ROOM (TOO EASY!). 
  
      IF(LOOP2.EQ.PYRAM.AND.(LOC.EQ.PLAC(PYRAM) 
     $.OR.LOC.EQ.PLAC(EMRALD)))GOTO 6020
      IF (LOOP2.EQ.RING.OR.LOOP2.EQ.MAIL) GOTO 6020 
      IF(TOTING(LOOP2))GOTO 6022
 6020 IF(HERE(LOOP2))K=1
      IF(TALLY.EQ.TALLY2+1.AND.K.EQ.0.AND.PLACE(CHEST).EQ.0 
     $.AND.HERE(LAMP).AND.PROP(LAMP).EQ.1)GOTO 6025 
      IF(ODLOC(8).NE.DLOC(8).AND.PCT(20))CALL RSPEAK(127) 
      GOTO 6030 
  
 6022 CALL RSPEAK(128)
  
***DON'T STEAL CHEST BACK FROM TROLL! 
  
      IF(PLACE(MESSAG).EQ.0)CALL MOVE(CHEST,CHLOC)
      CALL MOVE(MESSAG,CHLOC2)
      DO 6023 LOOP2=TRSURE,MAXTRS 
      IF(LOOP2.EQ.PYRAM.AND.(LOC.EQ.PLAC(PYRAM) 
     $.OR.LOC.EQ.PLAC(EMRALD)))GOTO 6023
      IF (LOOP2.EQ.RING.OR.LOOP2.EQ.MAIL) GOTO 6023 
      IF(AT(LOOP2).AND.FIXED(LOOP2).EQ.0)CALL CARRY(LOOP2,LOC)
      IF(TOTING(LOOP2))CALL DROP(LOOP2,CHLOC) 
 6023 CONTINUE
 6024 DLOC(8)=CHLOC 
      ODLOC(8)=CHLOC
      DSEEN(8)=.FALSE.
      GOTO 6030 
  
 6025 CALL RSPEAK(186)
      CALL MOVE(CHEST,CHLOC)
      CALL MOVE(MESSAG,CHLOC2)
      GOTO 6024 
  
***THIS THREATENING LITTLE DWARF IS IN THE ROOM WITH HIM! 
  
 6027 DTOTAL=DTOTAL+1 
      IF(ODLOC(LOOP).NE.DLOC(LOOP))GOTO 6030
      ATTACK=ATTACK+1 
      IF(KNFLOC.GE.0)KNFLOC=LOC 
  
***IF A DWARF GETS HIM, THE DWARF RUNS AWAY.
  
      IF(random(1000).GE.95*(DFLAG-2)) GOTO 6030 
      STICK=STICK+1 
      DLOC(LOOP)=random(8)+MISTS 
      IF(WIZKID) DLOC(LOOP)=0 
 6030 CONTINUE
  
***NOW WE KNOW WHAT'S HAPPENING.  LET'S TELL THE POOR SUCKER ABOUT IT.
  
      IF(DTOTAL.EQ.0)GOTO 2000
      IF(DTOTAL.EQ.1)GOTO 75
      WRITE(6,67)DTOTAL 
   67 FORMAT(/,' There are ',I1,' threatening little dwarves in the'
     $,' room with you.') 
      GOTO 77 
   75 CALL RSPEAK(4)
   77 IF(ATTACK.EQ.0)GOTO 2000
      IF(DFLAG.EQ.2)DFLAG=3 
  
***IF SAVED NOT = -1, HE BYPASSED THE 'START' CALL.  DWARVES GET *VERY* MAD.
  
      IF(SAVED.NE.-1)DFLAG=20 
      IF(ATTACK.EQ.1)GOTO 79
      WRITE(6,78)ATTACK 
   78 FORMAT(/,' ',I1,' of them throw knives at you!')
      K=6 
   82 IF (WIZKID) GOTO 88 
      IF(WEARING(1).OR.WEARING(2)) STICK=0
      IF(STICK.GT.1)GOTO 83 
      CALL RSPEAK(K+STICK)
      IF(STICK.EQ.0)GOTO 2000 
      GOTO 84 
   83 WRITE(6,68)STICK
   68 FORMAT(/,' ',I1,' of them get you!')
   84 OLDLC2=LOC
      GOTO 99 
  
   88 IF(STICK .NE. 0) WRITE(6,89) STICK
   89 FORMAT(/' ',I2,' of the dwarves just died for attacking',
     $' a wizard!')
      GOTO 2000 
  
   79 CALL RSPEAK(5)
      K=52
      GOTO 82 
***                      *******************
*                        ***** REPEAT ******
*                        *******************
* 
*  DESCRIBE THE CURRENT LOCATION AND (MAYBE) GET NEXT COMMAND.
* 
*  PRINT TEXT FOR CURRENT LOC.
  
 2000 IF(LOC.EQ.0)GOTO 99 
      IF(ROW) CALL MOVE(BOAT,LOC) 
      ROW=.FALSE. 
      KK=STEXT(LOC) 
      IF(MOD(ABB(LOC),ABBNUM).EQ.0.OR.KK.EQ.0)KK=LTEXT(LOC) 
      IF(FORCED(LOC).OR..NOT.DARK(0))GOTO 2001
      IF(WZDARK.AND.PCT(35))GOTO 90 
      KK=RTEXT(16)
 2001 IF(TOTING(BEAR))CALL RSPEAK(141)
      CALL SPEAK(KK)
      K=1 
      IF(FORCED(LOC))GOTO 3000
      IF(LOC.EQ.42.AND.DARK(0).AND..NOT.CLOSNG)CALL RSPEAK(8) 
  
***PRINT OUT DESCRIPTIONS OF OBJECTS AT THIS LOCATION.  IF NOT CLOSING AND
*  PROPERTY VALUE IS NEGATIVE, TALLY OFF ANOTHER TREASURE.  RUG IS SPECIAL
*  CASE; ONCE SEEN, ITS PROP IS 1 (DRAGON ON IT) TILL DRAGON IS KILLED. 
*  SIMILARLY FOR CHAIN; PROP IS INITIALLY 1 (LOCKED TO BEAR).  THESE HACKS
*  ARE BECAUSE PROP=0 IS NEEDED TO GET FULL SCORE.
  
      IF(DARK(0))GOTO 2012
      ABB(LOC)=ABB(LOC)+1 
      I=ATLOC(LOC)
 2004 IF(I.EQ.0)GOTO 2012 
      OBJ=I 
      IF(OBJ.GT.100)OBJ=OBJ-100 
      IF(OBJ.EQ.STEPS.AND.TOTING(NUGGET))GOTO 2008
      IF(PROP(OBJ).GE.0)GOTO 2006 
      IF(CLOSED)GOTO 2008 
      PROP(OBJ)=0 
      IF(OBJ.EQ.RUG.OR.OBJ.EQ.CHAIN)PROP(OBJ)=1 
      TALLY=TALLY-1 
  
*  IF REMAINING TREASURES TOO ELUSIVE, ZAP HIS LAMP.
  
      IF(TALLY.EQ.TALLY2.AND.TALLY.NE.0)LIMIT=MIN0(35,LIMIT)
 2006 KK=PROP(OBJ)
      IF(OBJ.EQ.STEPS.AND.LOC.EQ.FIXED(STEPS))KK=1
      CALL PSPEAK(OBJ,KK) 
 2008 I=LINK(I) 
      GOTO 2004 
  
 2009 K=54
 2010 SPK=K 
 2011 CALL RSPEAK(SPK)
 2012 VERB=0
      OBJ=0 
  
***MOVE MONGOOSE IF HE'S LOOSE. 
  
      IF(.NOT.LOOSE) GOTO 2600
 2550 IF(PLACE(TRAP).NE.PLACE(MONGOOS)) GOTO 2525 
      LOOSE=.FALSE. 
      IF(LOC.EQ.PLACE(TRAP)) CALL RSPEAK(229) 
      PROP(MONGOOS)=1 
      PROP(TRAP)=1
      GOTO 2600 
 2525 CALL MOVE(MONGOOS,(random(18)+15)) 
      IF(.NOT.HERE(MONGOOS)) GOTO 2600
      CALL RSPEAK(60) 
  
***THE QUICK MONGOOSE RUNS AWAY...     90% OF THE TIME. 
  
 2500 IF(PCT(10)) GOTO 2550 
      CALL MOVE(MONGOOS,(random(18)+15)) 
      CALL RSPEAK(61) 
  
***CHECK IF THIS LOC IS ELIGIBLE FOR ANY HINTS.  IF BEEN HERE LONG ENOUGH 
*  BRANCH TO HELP SECTION (ON LATER PAGE).  HINTS ALL COME BACK HERE EVERY TIME 
*  TO FINISH THE LOOP.  IGNORE 'HINTS' < 4 (SPECIAL STUFF, SEE DATABASE DESCRIPTION)
  
 2600 DO 2601 LOOP=4,HNTMAX 
      HINT=LOOP 
      IF(HINTED(HINT))GOTO 2602 
      IF(.NOT.BITSET(LOC,HINT))HINTLC(HINT)=-1
      HINTLC(HINT)=HINTLC(HINT)+1 
      IF(HINTLC(HINT).GE.HINTS(HINT,1))GOTO 40000 
 2601 CONTINUE
 2602 CONTINUE
  
***KICK THE RANDOM NUMBER GENERATOR JUST TO ADD VARIETY TO THE CHASE.  AND
*  IF CLOSING TIME, CHECK FOR ANY OBJECTS BEING TOTED WITH PROP < 0 AND SET 
*  THE PROP TO -1-PROP.  THIS WAY OBJECTS WON'T BE DESCRIBED UNTIL THEY'VE
*  BEEN PICKED UP AND PUT DOWN SEPARATE FROM THEIR RESPECTIVE PILES.  DON'T 
*  TICK CLOCK1 UNLESS WELL INTO CAVE (AND NOT AT Y2). 
  
      IF(.NOT.CLOSED)GOTO 2605
      IF(PROP(OYSTER).LT.0.AND.TOTING(OYSTER))
     $CALL PSPEAK(OYSTER,1) 
      DO 2604 LOOP=1,100
 2604 IF(TOTING(LOOP).AND.PROP(LOOP).LT.0)PROP(LOOP)=-1-PROP(LOOP)
 2605 WZDARK=DARK(0)
      IF(KNFLOC.GT.0.AND.KNFLOC.NE.LOC)KNFLOC=0 
      I=random(1)
      CALL GETIN(WD1,WD2,WCOUNT)
***EVERY INPUT, CHECK 'FOOBAR' FLAG.  IF ZERO, NOTHING'S GOING ON.  IF POS
*  MAKE NEG.  IF NEG, HE SKIPPED A WORD, SO MAKE IT ZERO. 
  
 2608 FOOBAR=MIN0(0,-FOOBAR)
  
***THIS IS WHERE RESTORE WOULD COME, IF IT WAS WORKING. 
      IF(TURNS.EQ.0.AND.WD1.EQ.'RESTORE   ')GOTO 8400 
  
 2606 TURNS=TURNS+1 
  
      IF(TURNS.LT.WAITNG) GOTO 2607 
      IF(TURNS.LT.OOPS) CALL RSPEAK(220)
      IF(TURNS.LT.OOPS) GOTO 2607 
      CALL RSPEAK(221)
      BFULL=.FALSE. 
      WAITNG=9999999
      OOPS=9999999
 2607 IF(TURNS.EQ.3) XXM=MINPMID(0) 
      IF(TURNS.NE.45)GOTO 2609
  
***CHECK IF PLAYER HAS ZAPPED TIMING ROUTINE;  IF SO, HE'S CHEATING.
  
      YYM=MINPMID(0)
      IF(XXM.EQ.YYM) SAVED=0
  
 2609 IF(VERB.EQ.SAY.AND.WD2.NE.' ')VERB=0
      IF(VERB.EQ.SAY)GOTO 4090
      IF(TALLY.EQ.0.AND.LOC.GE.MISTS.AND.LOC.NE.42)CLOCK1=CLOCK1-1
      IF(CLOCK1.EQ.0)GOTO 10000 
      IF(CLOCK1.LT.0)CLOCK2=CLOCK2-1
      IF(CLOCK2.EQ.0)GOTO 11000 
      IF(PROP(LAMP).EQ.1)LIMIT=LIMIT-1
      IF(LIMIT.LE.30.AND.HERE(BATTER).AND.PROP(BATTER).EQ.0 
     $.AND.HERE(LAMP))GOTO 12000
      IF(LIMIT.EQ.0)GOTO 12400
      IF(LIMIT.LT.0.AND.LOC.LE.8)GOTO 12600 
      IF(LIMIT.LE.30)GOTO 12200 
19999 IF(WD1.EQ.'ENTER     '.AND.
     $  (WD2.EQ.'STREAM    '.OR.WD2.EQ.'WATER     '.OR.
     $   WD2.EQ.'RIVER     ')) WD1='SWIM      '
      IF(WD1.EQ.'ENTER     '.AND.WD2.NE.' ')GOTO 2800 
      IF((WD1.NE.'WATER     '.AND.WD1.NE.'OIL       ')
     $.OR.(WD2.NE.'PLANT     '.AND.WD2.NE.'DOOR      ')) GOTO 2610 
      IF(AT(VOCAB(WD2,1)))WD2='POUR      '
 2610 IF(WD1.NE.'WEST      ')GOTO 2630
      IWEST=IWEST+1 
      IF(IWEST.EQ.10)CALL RSPEAK(17)
 2630 I=VOCAB(WD1,-1) 
      text (1:10) = wd1
      IF(I.EQ.-1)GOTO 2900
      K=MOD(I,1000) 
      KQ=I/1000+1 
      GOTO (3000,5000,4000,2010), KQ
***       LOC,OBJ.,VERB,REPEAT
      CALL BUG(22)
  
***GET SECOND WORD FOR ANALYSIS.
  
 2800 WD1=WD2 
      WD2=' ' 
      GOTO 2610 
  
***GEE, I DON'T UNDERSTAND. 
  
 2900 WRITE(6,2910) WD1 
 2910 FORMAT(/,' I don''t understand ''',A10,'''.') 
      GOTO 2600 
  
***                      *******************
*                        **** LOCATIONS ****
*                        *******************
*  FIGURE OUT THE NEW LOCATION
* 
*  GIVEN THE CURRENT LOCATION IN 'LOC', AND A MOTION VERB NUMBER IN 'K', AND
*  THE NEW LOCATION IN 'NEWLOC'.  THE CURRENT LOC IS SAVED IN 'OLDLOC' IF 
*  HE WANTS TO RETREAT.  THE CURRENT OLDLOC IS SAVED IN OLDLC2, IN CASE HE
*  DIES.  (IF HE DOES, NEWLOC WILL BE LIMBO, AND OLDLOC WILL BE WHAT KILLED 
*  HIM, SO WE NEED OLDLC2, WHICH IS THE LAST PLACE HE WAS SAFE.)
  
 3000 KK=KEY(LOC) 
      NEWLOC=LOC
      IF(KK.EQ.0)CALL BUG(26) 
      IF(K.EQ.NULL)GOTO 2 
      IF(K.EQ.BACK)GOTO 20
      IF(K.EQ.LOOK)GOTO 30
      IF(K.EQ.SIT)GOTO 35 
      IF(K.EQ.CAVE)GOTO 40
      IF(K.EQ.GO2)GOTO 45 
      OLDLC2=OLDLOC 
      OLDLOC=LOC
  
    9 LL=IABS(TRAVEL(KK)) 
      IF(MOD(LL,1000).EQ.1.OR.MOD(LL,1000).EQ.K)GOTO 10 
      IF(TRAVEL(KK).LT.0)GOTO 50
      KK=KK+1 
      GOTO 9
  
   10 LL=LL/1000
   11 NEWLOC=LL/1000
      K=MOD(NEWLOC,100) 
      IF(NEWLOC.LE.300)GOTO 13
      IF(PROP(K).NE.NEWLOC/100-3)GOTO 16
   12 IF(TRAVEL(KK).LT.0)CALL BUG(25) 
      KK=KK+1 
      NEWLOC=IABS(TRAVEL(KK))/1000
      IF(NEWLOC.EQ.LL)GOTO 12 
      LL=NEWLOC 
      GOTO 11 
  
   13 IF(NEWLOC.LE.100)GOTO 14
      IF(TOTING(K).OR.(NEWLOC.GT.200.AND.AT(K)))GOTO 16 
      GOTO 12 
  
   14 IF(NEWLOC.NE.0.AND..NOT.PCT(NEWLOC))GOTO 12 
   16 NEWLOC=MOD(LL,1000) 
      IF(NEWLOC.LE.300)GOTO 2 
      IF(NEWLOC.LE.500)GOTO 30000 
      CALL RSPEAK(NEWLOC-500) 
      NEWLOC=LOC
      GOTO 2
  
***SPECIAL MOTIONS COME HERE.  LABELLING CONVENTION: STATEMENT NUMBERS N
*  (XX=00-99) ARE USED FOR SPECIAL CASE NUMBER NNN (NNN=301-500). 
  
30000 NEWLOC=NEWLOC-300 
      IF(NEWLOC.LE.0.OR.NEWLOC.GT.5) CALL BUG(20) 
      GOTO(30100,30200,30300,30400,30500),NEWLOC
  
***TRAVEL 301.  PLOVER-ALCOVE PASSAGE.  CAN CARRY ONLY EMERALD.  NOTE: T
*  TABLE MUST INCLUDE 'USELESS' ENTRIES GOING THROUGH PASSAGE, WHICH CAN
*  BE USED FOR ACTUAL MOTION, BUT CAN BE SPOTTED BY 'GO BACK'.
  
30100 NEWLOC = (2 * PLAC(EMERALD) - 1 ) - LOC 
      IF(HOLDNG.EQ.0.OR.(HOLDNG.EQ.1.AND.TOTING(EMRALD)))GOTO 2 
      NEWLOC=LOC
      CALL RSPEAK(117)
      GOTO 2
  
***TRAVEL 302.  PLOVER TRANSPORT.  DROP THE EMERALD (ONLY USE SPECIAL TR
*  TOTING IT), SO HE'S FORCED TO USE THE PLOVER-PASSAGE TO GET IT OUT.
*  DROPPED IT, GO BACK AND PRETEND HE WASN'T CARRYING IT AFTER ALL. 
  
30200 CALL DROP(EMRALD,LOC) 
      GOTO 12 
  
***TRAVEL 303.  TROLL BRIDGE.  MUST BE DONE ONLY AS SPECIAL MOTION SO TH
*  DWARVES WON'T WANDER ACROSS AND ENCOUNTER THE BEAR.  (THEY WON'T FOLL
*  PLAYER THERE BECAUSE THAT REGION IS FORBIDDEN TO THE PIRATE.)  IF
*  PROP(TROLL)=1, HE'S CROSSED SINCE PAYING, SO STEP OUT AND BLOCK HIM. 
*  (STANDARD TRAVEL ENTRIES CHECK FOR PROP(TROLL)=0.)  SPECIAL STUFF FOR
  
30300 IF(PROP(TROLL).NE.1)GOTO 30310
      CALL PSPEAK(TROLL,1)
      PROP(TROLL)=0 
      CALL MOVE(TROLL2,0) 
      CALL MOVE(TROLL2+100,0) 
      CALL MOVE(TROLL,PLAC(TROLL))
      CALL MOVE(TROLL+100,FIXD(TROLL))
      CALL JUGGLE(CHASM)
      NEWLOC=LOC
      GOTO 2
  
30310 NEWLOC=PLAC(TROLL)+FIXD(TROLL)-LOC
      IF(PROP(TROLL).EQ.0)PROP(TROLL)=1 
      IF(.NOT.TOTING(BEAR))GOTO 2 
      CALL RSPEAK(162)
      PROP(CHASM)=1 
      PROP(TROLL)=2 
      CALL DROP(BEAR,NEWLOC)
      FIXED(BEAR)=-1
      PROP(BEAR)=3
      IF(PROP(SPICES).LT.0)TALLY2=TALLY2+1
      OLDLC2=NEWLOC 
      GOTO 99 
  
***TRAVEL 304.  ROWING THE BOAT.  SINCE HE CAN'T CARRY THE BOAT HIMSELF,
*  (IT'S TOO HEAVY) WE MUST MOVE THE BOAT FOR HIM.
  
30400 ROW=.TRUE.
      GOTO 12 
  
***TRAVEL 305.  CROSSING THE FRAYED ROPE... IT MIGHT BREAK. 
  
30500 IF(PROP(ROPE).EQ.1) GOTO 30510
      IF(PCT(60)) GOTO 30510
      PROP(ROPE)=2
      CALL RSPEAK(225)
      NEWLOC=20 
      GOTO 2
  
30510 OLDLC2=OLDLOC 
      OLDLOC=LOC
      NEWLOC=PLAC(ROPE)+FIXD(ROPE)-OLDLOC 
      GOTO 2
  
***END OF SPECIALS. 
  
***HANDLE 'GO BACK'.  LOOK FOR VERB WHICH GOES FROM LOC TO OLDLOC, OR TO
*  IF OLDLOC HAS FORCED-MOTION.  K2 SAVES ENTRY -> FORCED LOC -> PREVIOU
  
   20 K=OLDLOC
      IF(FORCED(K))K=OLDLC2 
      OLDLC2=OLDLOC 
      OLDLOC=LOC
      K2=0
      IF(K.NE.LOC)GOTO 21 
      CALL RSPEAK(91) 
      GOTO 2
  
   21 LL=MOD((IABS(TRAVEL(KK))/1000),1000)
      IF(LL.EQ.K)GOTO 25
      IF(LL.GT.300)GOTO 22
      J=KEY(LL) 
      IF(FORCED(LL).AND.MOD((IABS(TRAVEL(J))/1000),1000).EQ.K)K2=KK 
   22 IF(TRAVEL(KK).LT.0)GOTO 23
      KK=KK+1 
      GOTO 21 
  
   23 KK=K2 
      IF(KK.NE.0)GOTO 25
      CALL RSPEAK(140)
      GOTO 2
  
   25 K=MOD(IABS(TRAVEL(KK)),1000)
      KK=KEY(LOC) 
      GOTO 9
  
***LOOK.  CAN'T GIVE MORE DETAIL.  PRETEND IT WASN'T DARK (THOUGH IT MAY
*  BE DARK) SO HE WON'T FALL INTO A PIT WHILE STARING INTO THE GLOOM. 
  
   30 IF(DETAIL.LT.3)CALL RSPEAK(15)
      DETAIL=DETAIL+1 
      WZDARK=.FALSE.
      ABB(LOC)=0
      GOTO 2
  
***    SIT. HE SITS ON THE GROUND,
*      OR IF HE TRIES THE CHAIR,
*      HE DIES. 
* 
   35 IF ( WIZKID ) GOTO 37 
      IF ( HERE(CHAIR) ) GOTO 36
      SPK = 232 
      GOTO 2011 
  
   36 CALL RSPEAK (233) 
      NUMDIE = MAXDIE 
      GOTO 99 
  
   37 SPK = 54
      GOTO 2011 
  
***CAVE.  DIFFERENT MESSAGES DEPENDING ON WHETHER ABOVE GROUND. 
  
   40 IF(LOC.LT.8)CALL RSPEAK(57) 
      IF(LOC.GE.8)CALL RSPEAK(58) 
      GOTO 2
  
***GOTO. LETS A WIZARD GO ANYWHERE HE WANTS.
  
   45 IF(.NOT.WIZKID) GOTO 2900 
      IF(WD2.EQ.' ') GOTO 2900
      WD1=WD2 
      WD2=' '
      WHERE=0 
      DO 46 LOOP=1,3
      IF(WD1(LOOP:LOOP).LT.'0'.OR.WD1(LOOP:LOOP).GT.'9') GOTO 46
      WHERE=(WHERE*10)+(ICHAR(WD1(LOOP:LOOP)) - ICHAR('0'))
   46 CONTINUE
      IF(WHERE.GT.0.AND.WHERE.LE.LOCSIZ) GOTO 47
      I=VOCAB(WD1,-1) 
  
***IF LOCATION IS NOT A ROOM NUMBER THE WORD MUST BE AN OBJECT. 
  
      IF(I/1000.NE.1) GOTO 2900 
      IF(I-1000 .EQ. PIRATE) GOTO 48
      IF(PLACE(I-1000).EQ.0) GOTO 2900
      WHERE=PLACE(I-1000) 
   47 OLDLC2=OLDLOC 
      OLDLOC=LOC
      NEWLOC=WHERE
      GOTO 2
  
   48 WHERE=DLOC(8) 
      IF(WHERE .NE. 0) GOTO 47
      WRITE(6,49) 
   49 FORMAT (' The pirate is dead!') 
      GOTO 2600 
  
***NON-APPLICABLE MOTION.  VARIOUS MESSAGES DEPENDING ON WORD GIVEN.
  
   50 SPK=12
      IF(K.GE.43.AND.K.LE.50)SPK=9
      IF(K.EQ.29.OR.K.EQ.30)SPK=9 
      IF(K.EQ.7.OR.K.EQ.36.OR.K.EQ.37)SPK=10
      IF(K.EQ.11.OR.K.EQ.19)SPK=11
      IF(VERB.EQ.FIND.OR.VERB.EQ.INVENT)SPK=59
      IF(K.EQ.62.OR.K.EQ.65)SPK=42
      IF(K.EQ.17)SPK=80 
      CALL RSPEAK(SPK)
      GOTO 2
*** 
* YOU ARE NOW SLIGHTLY DEAD.
* 
*  IF THE CURRENT LOC IS ZERO, IT MEANS THE CLOWN GOT HIMSELF KILLED.  We
*  ALLOW THIS MAXDIE TIMES.  MAXDIE IS AUTOMATICALLY SET BASED ON THE NUMBER
*  SNIDE MESSAGES AVAILABLE.  EACH DEATH RESULTS IN A MESSAGE (81, 83, E
*  WHICH OFFERS REINCARNATION; IF ACCEPTED, THIS RESULTS IN MESSAGE 82, 
*  ETC.  THE LAST TIME, IF HE WANTS ANOTHER CHANCE, HE GETS A SNIDE REMA
*  WE EXIT.  WHEN REINCARNATED, ALL OBJECTS BEING CARRIED GET DROPPED AT
*  (PRESUMABLY THE LAST PLACE PRIOR TO BEING KILLED) WITHOUT CHANGE OF P
*  THE LOOP RUNS BACKWARDS TO ASSURE THAT THE MONGOOS IS DROPPED BEFORE THE 
*  (THIS KLUGE COULD BE CHANGED ONCE WE'RE SURE ALL REFERENCES TO MONGOOS A 
*  ARE DONE BY KEYWORDS.)  THE LAMP IS A SPECIAL CASE (IT WOULDN'T DO TO
*  IT IN THE CAVE).  IT IS TURNED OFF AND LEFT OUTSIDE THE BUILDING (ONL
*  WAS CARRYING IT, OF COURSE).  HE HIMSELF IS LEFT INSIDE THE BUILDING 
*  HEAVEN HELP HIM IF HE TRIES TO ZZZZADV BACK INTO THE CAVE WITHOUT THE L
*  OLDLOC IS ZAPPED SO HE CAN'T JUST 'RETREAT'. 
* 
*  THE EASIEST WAY TO GET KILLED IS TO FALL INTO A PIT IN PITCH DARKNESS
  
   90 CALL RSPEAK(23) 
      OLDLC2=LOC
  
***OKAY, HE'S DEAD.  LET'S GET ON WITH IT.
  
   99 IF (.NOT.WIZKID) GOTO 91
      CALL RSPEAK(224)
      LOC=OLDLC2
      GOTO 2000 
  
   91 IF(CLOSNG)GOTO 95 
      NUMDIE=NUMDIE+1 
      I=83
      J=84
      IF(NUMDIE.NE.1) GOTO 92 
      I=81
      J=82
   92 IF(NUMDIE.LE.MAXDIE) GOTO 93
      I=85
      J=86
   93 IF(NUMDIE.EQ.MAXDIE.OR..NOT.YES(I,J,54,WCOUNT)) GOTO 20000
      IF(OLDLOC.EQ.FIXD(TROLL)) OLDLC2=PLAC(TROLL)
      DO 98 LOOP=1,100
      I=101-LOOP
      IF(.NOT.TOTING(I))GOTO 98 
      K=OLDLC2
      CALL DROP(I,K)
      IF(I.EQ.MONGOOS) LOOSE=.TRUE. 
   98 CONTINUE
      IF(LOC.LT.MISTS) GOTO 100 
      LOC=OLDLC2
      OLDLOC=LOC
      GOTO 2000 
  
  100 LOC=3 
      OLDLOC=LOC
      IF(PLACE(LAMP).NE.OLDLC2) GOTO 2000 
      CALL MOVE(LAMP,1) 
      PROP(LAMP)=0
      GOTO 2000 
  
***HE DIED DURING CLOSING TIME.  NO RESURRECTION.  TALLY UP A DEATH AND 
  
   95 CALL RSPEAK(131)
      NUMDIE=NUMDIE+1 
      GOTO 20000
  
***                      *******************
*                        ****** VERBS ******
*                        *******************
*  ANALYSE A VERB.  REMEMBER WHAT IT WAS, GO BACK FOR OBJECT IF SECOND W
*  UNLESS VERB IS 'SAY' OR 'SUSPEND', WHICH SNARFS ARBITRARY SECOND WORD
  
 4000 VERB=K
      SPK=ACTSPK(VERB)
      IF(WD2.NE.' '.AND.
     $      (VERB.NE.SAY.AND.VERB.NE.SUSPND))GOTO 2800
      IF(VERB.EQ.SAY.OR.VERB.EQ.SUSPND)OBJ=-1
***IF HE WANTS LIGHT, LET HIM TRY TO FLIP THE SWITCH. 
  
      IF(OBJ.EQ.SWITCH) GOTO 5150 
      IF(OBJ.NE.0)GOTO 4090 
  
***ANALYSE AN INTRANSITIVE VERB (IE, NO OBJECT GIVEN YET).
  
      GOTO(8010,8000,8000,8040,2009,8040,9070,9080,8000,8000, 
     $2011,9120,9130,8140,9150,8000,8000,8180,8000,8200,
     $8000,9220,9230,8240,8250,8260,8270,8000,8000,8300,
     $8310,8320,8330),VERB
*          TAKE DROP  SAY OPEN NOTH LOCK   ON  OFF WAVE CALM
*     WALK KILL POUR  EAT DRNK  RUB TOSS QUIT FIND INVN 
*     FEED FILL BLST SCOR  FOO  BRF READ BREK WAKE SUSP 
*     WEAR PISS SWIM
      CALL BUG(23)
  
***ANALYSE A TRANSITIVE VERB. 
  
 4090 GOTO(9010,9020,9030,9040,2009,9040,9070,9080,9090,2011, 
     $2011,9120,9130,9140,9150,9160,9170,2011,9190,9190,
     $9210,9220,9230,2011,2011,2011,9270,9280,9290,8300,
     $9310,9320,9330),VERB
*          TAKE DROP  SAY OPEN NOTH LOCK   ON  OFF WAVE CALM
*     WALK KILL POUR  EAT DRNK  RUB TOSS QUIT FIND INVN 
*     FEED FILL BLST SCOR  FOO  BRF READ BREK WAKE SUSP 
*     WEAR PISS SWIM
      CALL BUG(24)
  
***ROUTINES FOR PERFORMING THE VARIOUS ACTION VERBS 
  
***STATEMENT NUMBERS IN THIS SECTION ARE 8000 FOR INTRANSITIVE VERBS, 90
*  TRANSITIVE, PLUS TEN TIMES THE VERB NUMBER.  MANY INTRANSITIVE VERBS 
*  TRANSITIVE CODE, AND SOME VERBS USE CODE FOR OTHER VERBS, AS NOTED BE
  
C  RANDOM INTRANSITIVE VERBS COME HERE.  CLEAR OBJ JUST IN CASE (SEE 'AT
  
 8000  WRITE(6,8002) WD1
 8002 FORMAT(/,' ',A10,x,'What?')
      GOTO 2600 
  
***CARRY, NO OBJECT GIVEN YET.  OK IF ONLY ONE OBJECT PRESENT.
  
 8010 IDUMY=ATLOC(LOC)
      IF(IDUMY.EQ.0.OR.LINK(IDUMY).NE.0) GOTO 8000
      DO 8012 LOOP=1,7
      IF(DLOC(LOOP).EQ.LOC.AND.DFLAG.GE.2)GOTO 8000 
 8012 CONTINUE
      OBJ=ATLOC(LOC)
  
***CARRY AN OBJECT.  SPECIAL CASES FOR MONGOOS AND SACK (IF MONGOOS IN SACK, C
*  TAKE ONE WITHOUT THE OTHER.  LIQUIDS ALSO SPECIAL, SINCE THEY DEPEND 
*  STATUS OF BOTTLE.  ALSO VARIOUS SIDE EFFECTS, ETC. 
  
 9010 IF(TOTING(OBJ))GOTO 2011
      TEM=LOC 
      IF(.NOT.WIZKID.OR.FIXED(OBJ).NE.0.OR.OBJ.EQ.WATER.OR.OBJ.EQ.OIL)
     $GOTO 9011 
      CALL MOVE(OBJ,LOC)
      IF((OBJ.EQ.SACK.OR.OBJ.EQ.MONGOOS).AND.PROP(SACK).EQ.1) 
     $CALL MOVE(SACK+MONGOOS-OBJ,LOC) 
 9011 SPK=25
      IF(OBJ.EQ.PLANT.AND.PROP(PLANT).LE.0)SPK=115
      IF(OBJ.EQ.BEAR.AND.PROP(BEAR).EQ.1)SPK=169
      IF(OBJ.EQ.CHAIN.AND.PROP(BEAR).NE.0)SPK=170 
      IF(PLAC(OBJ).EQ.PLAC(BOOK)) SPK=234 
      IF(FIXED(OBJ).NE.0)GOTO 2011
      IF(OBJ.NE.WATER.AND.OBJ.NE.OIL)GOTO 9017
      IF(HERE(BOTTLE).AND.LIQ(0).EQ.OBJ)GOTO 9018 
      OBJ=BOTTLE
      IF(TOTING(BOTTLE).AND.PROP(BOTTLE).EQ.1)GOTO 9220 
      IF(PROP(BOTTLE).NE.1)SPK=105
      IF(.NOT.TOTING(BOTTLE))SPK=104
      GOTO 2011 
 9018 OBJ=BOTTLE
 9017 IF(HOLDNG.LT.HLDMAX.OR.WIZKID)GOTO 9016 
      CALL RSPEAK(92) 
      GOTO 2012 
 9016 IF(OBJ.NE.MONGOOS)GOTO 9014 
      IF(PROP(MONGOOS).NE.0)GOTO 9014 
      IF(TOTING(SACK))GOTO 9015 
      CALL RSPEAK(27) 
      GOTO 2012 
 9015 PROP(MONGOOS)=1 
      PROP(SACK)=1
      LOOSE=.FALSE. 
 9014 IF((OBJ.EQ.MONGOOS.OR.OBJ.EQ.SACK).AND.PROP(MONGOOS).NE.0)
     $CALL CARRY(MONGOOS+SACK-OBJ,LOC)
      IF(HERE(TRAP)) PROP(TRAP)=0 
      CALL CARRY(OBJ,LOC) 
      IF(HERE(TRAP)) PROP(TRAP)=0 
      K=LIQ(0)
      IF(OBJ.EQ.BOTTLE.AND.K.NE.0)PLACE(K)=-1 
      GOTO 2009 
  
***DISCARD OBJECT.  'THROW' ALSO COMES HERE FOR MOST OBJECTS.  SPECIAL C
*  MONGOOS (MIGHT ATTACK SNAKE OR DRAGON) AND SACK (MIGHT CONTAIN MONGOOS) AND
*  DROP COINS AT VENDING MACHINE FOR EXTRA BATTERIES. 
  
 9020 IF(TOTING(ROD2).AND.OBJ.EQ.ROD.AND..NOT.TOTING(ROD))OBJ=ROD2
      IF(.NOT.TOTING(OBJ))GOTO 2011 
      IF(OBJ.EQ.RING) PROP(RING)=0
      IF(OBJ.EQ.RING) WEARING(1)=.FALSE.
      IF(OBJ.EQ.MAIL) PROP(MAIL)=0
      IF(OBJ.EQ.MAIL) WEARING(2)=.FALSE.
      IF(OBJ.NE.MONGOOS)GOTO 9024 
      PROP(MONGOOS)=0 
      PROP(SACK)=0
      LOOSE=.TRUE.
      IF(.NOT.HERE(SNAKE)) GOTO 9024
      CALL RSPEAK(30) 
      IF(CLOSED)GOTO 19000
      CALL MOVE(SNAKE,0)
  
***SET PROP FOR USE BY TRAVEL OPTIONS 
  
      PROP(SNAKE)=1 
 9021 K=LIQ(0)
      IF(K.EQ.OBJ)OBJ=BOTTLE
      IF(OBJ.EQ.BOTTLE.AND.K.NE.0)PLACE(K)=0
      IF(OBJ.EQ.SACK.AND.PROP(MONGOOS).NE.0)CALL DROP(MONGOOS,LOC)
      CALL DROP(OBJ,LOC)
      IF(OBJ.EQ.MONGOOS) CALL RSPEAK(61)
      IF(LOC.GE.117 .AND. LOC.LE.126)CALL MOVE(OBJ,125) 
      GOTO 2012 
  
 9024 IF(OBJ.NE.COINS.OR..NOT.HERE(VEND))GOTO 9025
      CALL MOVE(COINS,0)
      CALL DROP(BATTER,LOC) 
      CALL PSPEAK(BATTER,0) 
      GOTO 2012 
  
 9025 IF(OBJ.NE.MONGOOS.OR..NOT.AT(DRAGON).OR.PROP(DRAGON).NE.0)
     $GOTO 9026 
      CALL RSPEAK(154)
      CALL MOVE(MONGOOS,0)
      PROP(MONGOOS)=0 
      IF(PLACE(SNAKE).EQ.PLAC(SNAKE))TALLY2=TALLY2+1
      GOTO 2012 
  
 9026 IF(OBJ.NE.BEAR.OR..NOT.AT(TROLL))GOTO 9027
      CALL RSPEAK(163)
      CALL MOVE(TROLL,0)
      CALL MOVE(TROLL+100,0)
      CALL MOVE(TROLL2,PLAC(TROLL)) 
      CALL MOVE(TROLL2+100,FIXD(TROLL)) 
      CALL JUGGLE(CHASM)
      PROP(TROLL)=2 
      GOTO 9021 
  
 9027 IF(OBJ.EQ.VASE.AND.LOC.NE.PLAC(PILLOW))GOTO 9028
      CALL RSPEAK(54) 
      GOTO 9021 
  
 9028 PROP(VASE)=2
      IF(AT(PILLOW))PROP(VASE)=0
      CALL PSPEAK(VASE,PROP(VASE)+1)
      IF(PROP(VASE).NE.0)FIXED(VASE)=-1 
      GOTO 9021 
  
***SAY.  ECHO WD2 (OR WD1 IF NO WD2 (SAY WHAT?, ETC.).)  MAGIC WORDS OVE
  
 9030 IF(WD2.NE.'QUESTION  ') GOTO 9031 
      WIZKID=WIZARD(0)
      GOTO 2012 
 9031 IF(.NOT.WIZKID) GOTO 9032 
      IF(WD2.EQ.'WHERE') WRITE(6,9034) LOC 
 9034 FORMAT(' You are currently at location',I4,'.') 
      IF(WD2.NE.'CLOSING    ') GOTO 9032
      CLOSNG=.TRUE. 
      PANIC=.TRUE.
      CLOSED=.TRUE. 
      GOTO 2012 
 9032 IF(WD2.NE.' ')WD1=WD2 
      I=VOCAB(WD1,-1) 
      IF(I.EQ.62.OR.I.EQ.65.OR.I.EQ.71.OR.I.EQ.2025)GOTO 9035 
      WRITE(6,9033) WD1
 9033 FORMAT(/,' Okay, ',A10,'.') 
      GOTO 2012 
  
 9035 WD2=' ' 
      OBJ=0 
      GOTO 2630 
  
***LOCK, UNLOCK, NO OBJECT GIVEN.  ASSUME VARIOUS THINGS IF PRESENT.
  
 8040 SPK=28
      IF(HERE(SACK)) OBJ=SACK 
      IF(HERE(CLAM))OBJ=CLAM
      IF(HERE(OYSTER))OBJ=OYSTER
      IF(AT(DOOR))OBJ=DOOR
      IF(AT(GRATE))OBJ=GRATE
      IF(OBJ.NE.0.AND.HERE(CHAIN))GOTO 8000 
      IF(HERE(CHAIN))OBJ=CHAIN
      IF(OBJ.EQ.0)GOTO 2011 
  
***LOCK, UNLOCK OBJECT.  SPECIAL STUFF FOR OPENING CLAM/OYSTER AND FOR C
  
 9040 IF(OBJ.EQ.CLAM.OR.OBJ.EQ.OYSTER)GOTO 9046 
      IF(OBJ.EQ.SACK) GOTO 9041 
      IF(OBJ.EQ.DOOR)SPK=111
      IF(OBJ.EQ.DOOR.AND.PROP(DOOR).EQ.1)SPK=54 
      IF(OBJ.EQ.SACK)SPK=32 
      IF(OBJ.EQ.KEYS)SPK=55 
      IF(OBJ.EQ.GRATE.OR.OBJ.EQ.CHAIN)SPK=31
      IF(SPK.NE.31.OR..NOT.HERE(KEYS))GOTO 2011 
      IF(OBJ.EQ.CHAIN)GOTO 9048 
      IF(.NOT.CLOSNG)GOTO 9043
      K=130 
      IF(.NOT.PANIC)CLOCK2=15 
      PANIC=.TRUE.
      GOTO 2010 
  
***SACK.
  
 9041 IF(VERB.EQ.LOCK) GOTO 9042
      OBJ=MONGOOS 
      GOTO 9020 
 9042 SPK=225 
      GOTO 2011 
  
***GRATE. 
  
 9043 K=34+PROP(GRATE)
      PROP(GRATE)=1 
      IF(VERB.EQ.LOCK)PROP(GRATE)=0 
      K=K+2*PROP(GRATE) 
      GOTO 2010 
  
***CLAM/OYSTER. 
  
 9046 K=0 
      IF(OBJ.EQ.OYSTER)K=1
      SPK=124+K 
      IF(TOTING(OBJ))SPK=120+K
      IF(.NOT.TOTING(TRIDNT))SPK=122+K
      IF(VERB.EQ.LOCK)SPK=61
      IF(SPK.NE.124)GOTO 2011 
      CALL MOVE(CLAM,0) 
      CALL DROP(OYSTER,LOC) 
      CALL DROP(PEARL,PLAC(CLAM)+2) 
      GOTO 2011 
  
***CHAIN. 
  
 9048 IF(VERB.EQ.LOCK)GOTO 9049 
      SPK=171 
      IF(PROP(BEAR).EQ.0)SPK=41 
      IF(PROP(CHAIN).EQ.0)SPK=37
      IF(SPK.NE.171)GOTO 2011 
      PROP(CHAIN)=0 
      FIXED(CHAIN)=0
      IF(PROP(BEAR).NE.3)PROP(BEAR)=2 
      FIXED(BEAR)=2-PROP(BEAR)
      GOTO 2011 
  
 9049 SPK=172 
      IF(PROP(CHAIN).NE.0)SPK=34
      IF(LOC.NE.PLAC(CHAIN))SPK=173 
      IF(SPK.NE.172)GOTO 2011 
      PROP(CHAIN)=2 
      IF(TOTING(CHAIN))CALL DROP(CHAIN,LOC) 
      FIXED(CHAIN)=-1 
      GOTO 2011 
  
***LIGHT LAMP 
  
 9070 IF(.NOT.HERE(LAMP))GOTO 2011
      SPK=184 
      IF(LIMIT.LT.0)GOTO 2011 
      PROP(LAMP)=1
      CALL RSPEAK(39) 
      IF(WZDARK)GOTO 2000 
      GOTO 2012 
  
***LAMP OFF 
  
 9080 IF(.NOT.HERE(LAMP))GOTO 2011
      PROP(LAMP)=0
      CALL RSPEAK(40) 
      IF(DARK(0))CALL RSPEAK(16)
      GOTO 2012 
  
***WAVE.  NO EFFECT UNLESS WAVING ROD AT FISSURE. 
  
 9090 IF((.NOT.TOTING(OBJ)).AND.(OBJ.NE.ROD.OR..NOT.TOTING(ROD2)))
     $SPK=29
      IF(OBJ.NE.ROD.OR..NOT.AT(FISSUR).OR..NOT.TOTING(OBJ)) GOTO 9091 
      SPK=205 
      CALL MOVE(ROD,125)
      GOTO 2011 
 9091 IF(OBJ.NE.ROD.OR..NOT.AT(ROPE).OR..NOT. 
     $TOTING(OBJ).OR.CLOSNG) GOTO 2011
      IF(PROP(ROPE).GT.0) GOTO 9092 
      PROP(ROPE)=1
      CALL PSPEAK(ROPE,PROP(ROPE))
      GOTO 2011 
 9092 PROP(ROPE)=1-PROP(ROPE) 
      CALL PSPEAK(ROPE,PROP(ROPE))
      GOTO 2012 
  
***ATTACK.  ASSUME TARGET IF UNAMBIGUOUS.  'THROW' ALSO LINKS HERE.  ATT
* 
*  OBJECTS FALL INTO TWO CATEGORIES: ENEMIES (SNAKE, DWARF, ETC.)  AND O
*  (MONGOOS, CLAM).  AMBIGUOUS IF TWO ENEMIES, OR IF NO ENEMIES BUT TWO OTH 
  
 9120 DO 9121 LOOP=1,7
      I=LOOP
      IF(DLOC(LOOP).EQ.LOC.AND.DFLAG.GE.2)GOTO 9122 
 9121 CONTINUE
      I=0 
 9122 IF(OBJ.NE.0)GOTO 9124 
      IF(I.NE.0)OBJ=DWARF 
      IF(HERE(SNAKE))OBJ=OBJ*100+SNAKE
      IF(AT(DRAGON).AND.PROP(DRAGON).EQ.0)OBJ=OBJ*100+DRAGON
      IF(AT(TROLL))OBJ=OBJ*100+TROLL
      IF(HERE(BEAR).AND.PROP(BEAR).EQ.0)OBJ=OBJ*100+BEAR
      IF(OBJ.GT.100)GOTO 8000 
      IF(OBJ.NE.0)GOTO 9124 
  
***CAN'T ATTACK MONGOOS BY THROWING AXE.
  
      IF(HERE(MONGOOS).AND.VERB.NE.THROW)OBJ=MONGOOS
  
***CLAM AND OYSTER BOTH TREATED AS CLAM FOR INTRANSITIVE CASE; NO HARM D
  
      IF(HERE(CLAM).OR.HERE(OYSTER))OBJ=100*OBJ+CLAM
      IF(OBJ.GT.100)GOTO 8000 
 9124 IF(OBJ.NE.MONGOOS)GOTO 9125 
      SPK=137 
      IF(CLOSED)GOTO 2011 
      CALL MOVE(MONGOOS,0)
      PROP(MONGOOS)=0 
      IF(PLACE(SNAKE).EQ.PLAC(SNAKE))TALLY2=TALLY2+1
      SPK=45
 9125 IF(OBJ.EQ.0)SPK=44
      IF(OBJ.EQ.CLAM.OR.OBJ.EQ.OYSTER)SPK=150 
      IF(OBJ.EQ.SNAKE)SPK=46
      IF(OBJ.EQ.DWARF)SPK=48
      IF(OBJ.EQ.DWARF.AND.CLOSED)GOTO 19000 
      IF(OBJ.EQ.DRAGON)SPK=167
      IF(OBJ.EQ.TROLL)SPK=157 
      IF(OBJ.EQ.BEAR)SPK=165+(PROP(BEAR)+1)/2 
      IF(OBJ.NE.DRAGON.OR.PROP(DRAGON).NE.0)GOTO 2011 
  
***FUN STUFF FOR DRAGON.  IF HE INSISTS ON ATTACKING IT, WIN!  SET PROP 
*  MOVE DRAGON TO CENTRAL LOC (STILL FIXED), MOVE RUG THERE (NOT FIXED),
*  MOVE HIM THERE, TOO.  THEN DO A NULL MOTION TO GET NEW DESCRIPTION.
  
      CALL RSPEAK(49) 
      VERB=0
      OBJ=0 
      READ(5,9127) WD1
 9127 FORMAT(1A5) 
      call str$upcase (wd1,wd1)
      IF(WD1.NE.'Y' .AND.WD1.NE.'YES')GOTO 2608
      CALL PSPEAK(DRAGON,1) 
      PROP(DRAGON)=2
      PROP(RUG)=0 
      K=(PLAC(DRAGON)+FIXD(DRAGON))/2 
      CALL MOVE(DRAGON+100,-1)
      CALL MOVE(RUG+100,0)
      CALL MOVE(DRAGON,K) 
      CALL MOVE(RUG,K)
*  MOVE OBJECTS TO THE 'NEW' ROOM.
      DO 9126 LOOP=1,100
      IF(PLACE(LOOP).EQ.PLAC(DRAGON).OR.PLACE(LOOP).EQ.FIXD(DRAGON))
     $CALL MOVE(LOOP,K) 
 9126 CONTINUE
      LOC=K 
      K=NULL
      GOTO 3000 
  
***POUR.  IF NO OBJECT, OR OBJECT IS BOTTLE, ASSUME CONTENTS OF BOTTLE. 
*  SPECIAL TESTS FOR POURING WATER OR OIL ON PLANT OR RUSTY DOOR. 
  
 9130 IF(OBJ.EQ.BOTTLE.OR.OBJ.EQ.0)OBJ=LIQ(0) 
      IF(OBJ.EQ.0)GOTO 8000 
      IF(.NOT.TOTING(OBJ))GOTO 2011 
      SPK=78
      IF(OBJ.NE.OIL.AND.OBJ.NE.WATER)GOTO 2011
      PROP(BOTTLE)=1
      PLACE(OBJ)=0
      SPK=77
      IF(.NOT.(AT(PLANT).OR.AT(DOOR)))GOTO 2011 
  
      IF(AT(DOOR))GOTO 9132 
      SPK=112 
      IF(OBJ.NE.WATER)GOTO 2011 
 9131 CALL PSPEAK(PLANT,PROP(PLANT)+1)
      PROP(PLANT)=MOD(PROP(PLANT)+2,6)
      PROP(PLANT2)=PROP(PLANT)/2
      K=NULL
      GOTO 3000 
  
 9132 PROP(DOOR)=0
      IF(OBJ.EQ.OIL)PROP(DOOR)=1
      SPK=113+PROP(DOOR)
      GOTO 2011 
  
***EAT.  INTRANSITIVE: ASSUME FOOD IF PRESENT, ELSE ASK WHAT.  TRANSITIV
*  OK, SOME THINGS LOSE APPETITE, REST ARE RIDICULOUS.
  
 8140 IF(.NOT.HERE(FOOD))GOTO 8000
 8142 CALL MOVE(FOOD,0) 
      SPK=72
      GOTO 2011 
  
 9140 IF(OBJ.EQ.FOOD)GOTO 8142
      IF(OBJ.EQ.SHIT) GOTO 9145 
      IF(OBJ.EQ.COAL) GOTO 9148 
      IF(OBJ.EQ.MONGOOS.OR.OBJ.EQ.SNAKE.OR.OBJ.EQ.CLAM.OR.OBJ.EQ.OYSTER 
     $.OR.OBJ.EQ.DWARF.OR.OBJ.EQ.DRAGON.OR.OBJ.EQ.TROLL 
     $.OR.OBJ.EQ.BEAR)SPK=71
      GOTO 2011 
  
 9145 IF(PROP(SHIT).EQ.0) GOTO 9146 
      PROP(SHIT)=0
      SPK=225 
      GOTO 2011 
 9146 PROP(SHIT)=1
      SPK=226 
      GOTO 2011 
  
 9148 CALL MOVE(COAL,0) 
      SPK=227 
      GOTO 2011 
  
***DRINK.  IF NO OBJECT, ASSUME WATER AND LOOK FOR IT HERE.  IF WATER IS
*  THE BOTTLE, DRINK THAT, ELSE MUST BE AT A WATER LOC, SO DRINK STREAM.
  
 9150 IF(OBJ.EQ.0.AND.LIQLOC(LOC).NE.WATER.AND.(LIQ(0).NE.WATER 
     $.OR..NOT.HERE(BOTTLE)))GOTO 8000
      IF(OBJ.EQ.OIL) GOTO 9155
      IF(BFULL) SPK=209 
      IF(BFULL) GOTO 2011 
      IF(.NOT.HERE(FOUNTN)) GOTO 9155 
      IF(OBJ.EQ.POTION) GOTO 9151 
      BFULL=.TRUE.
      HLDMAX=7
      SPK=206 
      IF(PCT(50)) GOTO 2011 
 9151 HLDMAX=14 
      SPK=207 
      IF(PCT(40).OR.WIZKID) GOTO 2011 
      DO 9152 LOOP=1,100
 9152 IF(TOTING(LOOP)) CALL DROP(LOOP,LOC)
      HLDMAX=4
      SPK=208 
      IF(PCT(95)) GOTO 2011 
      GOTO 2011 
  
***OOPS... HE DRANK THE POTION OF INSTANT DEATH.

!  WARNING  - Following is dead code.
  
 9154 CALL RSPEAK(215)
      GOTO 99 
 9155 IF(OBJ.EQ.OIL)SPK=110 
      IF(OBJ.EQ.OIL) GOTO 2011
      IF(.NOT.(HERE(BOTTLE)).OR.PROP(BOTTLE).NE.0) GOTO 9157
      PROP(BOTTLE)=1
      SPK=74
 9157 BFULL=.TRUE.
      WAITNG=TURNS+25 
      OOPS=WAITNG+3 
      PLACE(WATER)=0
      GOTO 2011 
  
***RUB.  YIELDS VARIOUS SNIDE REMARKS.
  
 9160 IF(OBJ.NE.LAMP)SPK=76 
      GOTO 2011 
  
***THROW.  SAME AS DISCARD UNLESS AXE.  THEN SAME AS ATTACK EXCEPT IGNOR
*  AND IF DWARF IS PRESENT THEN ONE MIGHT BE KILLED.  (ONLY WAY TO DO SO
*  AXE ALSO SPECIAL FOR DRAGON, BEAR, AND TROLL.  TREASURES SPECIAL FOR 
  
 9170 IF(TOTING(ROD2).AND.OBJ.EQ.ROD.AND..NOT.TOTING(ROD))OBJ=ROD2
      IF(.NOT.TOTING(OBJ))GOTO 2011 
      IF(OBJ.GE.TRSURE.AND.OBJ.LE.MAXTRS.AND.AT(TROLL))GOTO 9178
      IF(OBJ.EQ.FOOD.AND.HERE(BEAR))GOTO 9177 
      IF(OBJ.NE.AXE)GOTO 9020 
      DO 9171 LOOP=1,7
  
*  NEEDN'T CHECK DFLAG IF AXE IS HERE.
  
      IF(DLOC(LOOP).EQ.LOC)GOTO 9172
 9171 CONTINUE
      SPK=152 
      IF(AT(DRAGON).AND.PROP(DRAGON).EQ.0)GOTO 9175 
      SPK=158 
      IF(AT(TROLL))GOTO 9175
      IF(HERE(BEAR).AND.PROP(BEAR).EQ.0)GOTO 9176 
      OBJ=0 
      GOTO 9120 
  
 9172 SPK=48
  
*  IF SAVED NOT = -1, HE BYPASSED THE 'START' CALL. 
  
      IF(random(2).EQ.0.OR.SAVED.NE.-1)GOTO 9175 
      DSEEN(I)=.FALSE.
      DLOC(I)=0 
      SPK=47
      DKILL=DKILL+1 
      IF(DKILL.EQ.1)SPK=149 
 9175 CALL RSPEAK(SPK)
      CALL DROP(AXE,LOC)
      K=NULL
      GOTO 3000 
  
*  THIS'LL TEACH HIM TO THROW THE AXE AT THE BEAR!
  
 9176 SPK=164 
      CALL DROP(AXE,LOC)
      FIXED(AXE)=-1 
      PROP(AXE)=1 
      CALL JUGGLE(BEAR) 
      GOTO 2011 
  
*  BUT THROWING FOOD IS ANOTHER STORY.
  
 9177 OBJ=BEAR
      GOTO 9210 
  
 9178 SPK=159 
  
*  SNARF A TREASURE FOR THE TROLL.
  
      CALL DROP(OBJ,0)
      CALL MOVE(TROLL,0)
      CALL MOVE(TROLL+100,0)
      CALL DROP(TROLL2,PLAC(TROLL)) 
      CALL DROP(TROLL2+100,FIXD(TROLL)) 
      CALL JUGGLE(CHASM)
      GOTO 2011 
  
***QUIT.  INTRANSITIVE ONLY.  VERIFY INTENT AND EXIT IF THAT'S WHAT HE W
  
 8180 GAVEUP=YES(22,54,54,WCOUNT) 
 8185 IF(GAVEUP)GOTO 20000
      GOTO 2012 
  
***FIND.  MIGHT BE CARRYING IT, OR IT MIGHT BE HERE.  ELSE GIVE CAVEAT. 
  
 9190 IF(AT(OBJ).OR.(LIQ(0).EQ.OBJ.AND.AT(BOTTLE))
     $.OR.K.EQ.LIQLOC(LOC))SPK=94 
      IF(WIZKID.AND.OBJ.EQ.DWARF) GOTO 9195 
      DO 9192 LOOP=1,7
 9192 IF(DLOC(LOOP).EQ.LOC.AND.DFLAG.GE.2.AND.OBJ.EQ.DWARF)SPK=94 
      IF(CLOSED)SPK=138 
      IF(TOTING(OBJ))SPK=24 
      GOTO 2011 
  
 9195 WRITE(6,9197) (DLOC(LOOP), LOOP=1,8)
 9197 FORMAT(' DWARF   1   2   3   4   5   6   7   PIRATE',/, 
     $' ROOM ',7I4,I7)
      GOTO 2012 
  
***INVENTORY.  IF OBJECT, TREAT SAME AS FIND.  ELSE REPORT ON CURRENT BU
  
 8200 SPK=98
      DO 8201 LOOP=1,100
      IF(LOOP.EQ.BEAR.OR..NOT.TOTING(LOOP))GOTO 8201
      IF(SPK.EQ.98)CALL RSPEAK(99)
      TEM=-1
      IF((LOOP.EQ.RING.AND.WEARING(1)).OR.(LOOP.EQ.MAIL.AND.WEARING(2)))
     $TEM=1 
      CALL PSPEAK(LOOP,TEM) 
      SPK=0 
 8201 CONTINUE
      IF(TOTING(BEAR))SPK=141 
      GOTO 2011 
  
***FEED.  IF MONGOOS, NOT HUNGRY.  SNAKE, DRAGON, TROLL: QUIP.  IF DWARF, MAKE
*  MAD.  BEAR, SPECIAL. 
  
 9210 IF(OBJ.NE.MONGOOS)GOTO 9212 
      SPK=100 
      GOTO 2011 
  
 9212 IF(OBJ.NE.SNAKE.AND.OBJ.NE.DRAGON.AND.OBJ.NE.TROLL)GOTO 9213
      SPK=102 
      IF(OBJ.EQ.DRAGON.AND.PROP(DRAGON).NE.0)SPK=110
      IF(OBJ.EQ.TROLL)SPK=182 
      IF(OBJ.NE.SNAKE.OR.CLOSED.OR..NOT.HERE(MONGOOS))GOTO 2011 
      SPK=101 
      CALL MOVE(MONGOOS,0)
      PROP(MONGOOS)=0 
      TALLY2=TALLY2+1 
      GOTO 2011 
  
 9213 IF(OBJ.NE.DWARF)GOTO 9214 
      IF(.NOT.HERE(COAL)) GOTO 8213 
      ONCE=.FALSE.
      DO 8212 LOOP=1,7
      IF(DLOC(LOOP).NE.LOC.OR.ONCE) GOTO 8212 
      ONCE=.TRUE. 
      CALL MOVE(COAL,0) 
      DLOC(LOOP)=0
 8212 CONTINUE
      SPK=117 
      IF(ONCE) SPK=116
      GOTO 2011 
 8213 IF(.NOT.HERE(FOOD))GOTO 2011
      SPK=103 
      DFLAG=DFLAG+1 
      GOTO 2011 
  
 9214 IF(OBJ.NE.BEAR)GOTO 9215
      IF(PROP(BEAR).EQ.0)SPK=102
      IF(PROP(BEAR).EQ.3)SPK=110
      IF(.NOT.HERE(FOOD))GOTO 2011
      CALL MOVE(FOOD,0) 
      PROP(BEAR)=1
      FIXED(AXE)=0
      PROP(AXE)=0 
      SPK=168 
      GOTO 2011 
  
 9215 SPK=14
      GOTO 2011 
  
***FILL.  BOTTLE MUST BE EMPTY, AND SOME LIQUID AVAILABLE.  (VASE IS NAS
  
 9220 IF(OBJ.EQ.VASE)GOTO 9222
      IF(OBJ.NE.0.AND.OBJ.NE.BOTTLE)GOTO 2011 
      IF(OBJ.EQ.0.AND..NOT.HERE(BOTTLE))GOTO 8000 
      SPK=107 
      IF(LIQLOC(LOC).EQ.0)SPK=106 
      IF(LIQ(0).NE.0)SPK=105
      IF(SPK.NE.107)GOTO 2011 
      PROP(BOTTLE)=MOD(COND(LOC),4)/2*2 
      K=LIQ(0)
      IF(TOTING(BOTTLE))PLACE(K)=-1 
      IF(K.EQ.OIL)SPK=108 
      GOTO 2011 
  
 9222 SPK=29
      IF(LIQLOC(LOC).EQ.0)SPK=144 
      IF(LIQLOC(LOC).EQ.0.OR..NOT.TOTING(VASE))GOTO 2011
      CALL RSPEAK(145)
      PROP(VASE)=2
      FIXED(VASE)=-1
      GOTO 9024 
  
***BLAST.  NO EFFECT UNLESS YOU'VE GOT DYNAMITE, WHICH IS A NEAT TRICK! 
  
 9230 IF(PROP(ROD2).LT.0.OR..NOT.CLOSED)GOTO 2011 
      BONUS=133 
      IF(LOC.EQ.115)BONUS=134 
      IF(HERE(ROD2))BONUS=135 
      CALL RSPEAK(BONUS)
      GOTO 20000
  
***SCORE.  GO TO SCORING SECTION, WHICH WILL RETURN TO 8241 IF SCORNG IS
  
 8240 SCORNG=.TRUE. 
      GOTO 20000
  
 8241 SCORNG=.FALSE.
      WRITE(6,8243)SCORE,MXSCOR 
 8243 FORMAT(/,' If you were to quit now, you would score',I4 
     $,' out of a possible',I4,'.') 
*     GAVEUP=YES(143,54,54,WCOUNT)
      GOTO 8185 
  
***FEE FIE FOE FOO (AND FUM).  ADVANCE TO NEXT STATE IF GIVEN IN PROPER 
*  LOOK UP WD1 IN SECTION 3 OF VOCAB TO DETERMINE WHICH WORD WE'VE GOT. 
*  WORD ZIPS THE EGGS BACK TO THE GIANT ROOM (UNLESS ALREADY THERE).
  
 8250 K=VOCAB(WD1,3)
      SPK=42
      IF(FOOBAR.EQ.1-K)GOTO 8252
      IF(FOOBAR.NE.0)SPK=151
      GOTO 2011 
  
 8252 FOOBAR=K
      IF(K.NE.4)GOTO 2009 
      FOOBAR=0
      IF(PLACE(EGGS).EQ.PLAC(EGGS)
     $.OR.(TOTING(EGGS).AND.LOC.EQ.PLAC(EGGS)))GOTO 2011
  
***BRING BACK TROLL IF WE STEAL THE EGGS BACK FROM HIM BEFORE CROSSING. 
  
      IF(PLACE(EGGS).EQ.0.AND.PLACE(TROLL).EQ.0.AND.PROP(TROLL).EQ.0) 
     $PROP(TROLL)=1 
      K=2 
      IF(HERE(EGGS))K=1 
      IF(LOC.EQ.PLAC(EGGS))K=0
      CALL MOVE(EGGS,PLAC(EGGS))
      CALL PSPEAK(EGGS,K) 
      GOTO 2012 
  
***BRIEF.  INTRANSITIVE ONLY.  SUPPRESS LONG DESCRIPTIONS AFTER FIRST TI
  
 8260 SPK=156 
      ABBNUM=10000
      DETAIL=3
      GOTO 2011 
  
***READ.  MAGAZINES IN DWARVISH, MESSAGE WE'VE SEEN, AND . . . OYSTER?
  
 8270 IF(HERE(MAGZIN)) OBJ=MAGZIN 
      IF(HERE(TABLET)) OBJ=OBJ*100+TABLET 
      IF(HERE(MESSAG)) OBJ=OBJ*100+MESSAG 
      IF(HERE(RUNES)) OBJ=RUNES 
      IF(HERE(BOOK)) OBJ=BOOK 
      IF(HERE(LISTING)) OBJ=LISTING 
      IF(CLOSED.AND.TOTING(OYSTER)) OBJ=OYSTER
      IF(OBJ.GT.100.OR.OBJ.EQ.0.OR.DARK(0))GOTO 8000
  
 9270 IF(DARK(0))GOTO 5190
      IF(OBJ.EQ.BOOK) SPK=231 
      IF(OBJ.EQ.LISTING) SPK=230
      IF(OBJ.EQ.MAGZIN) SPK=190 
      IF(OBJ.EQ.TABLET) SPK=196 
      IF(OBJ.EQ.MESSAG) SPK=191 
      IF(OBJ.EQ.RUNES) SPK=43 
      IF(OBJ.EQ.OYSTER.AND.HINTED(2).AND.TOTING(OYSTER)) SPK=194
      IF(OBJ.NE.OYSTER.OR.HINTED(2).OR..NOT.TOTING(OYSTER)
     $.OR..NOT.CLOSED)GOTO 2011 
      HINTED(2)=YES(192,193,54,WCOUNT)
      GOTO 2012 
  
***BREAK.  ONLY WORKS FOR MIRROR IN REPOSITORY AND, OF COURSE, THE VASE.
  
 9280 IF(OBJ.EQ.MIRROR)SPK=148
      IF(OBJ.EQ.VASE.AND.PROP(VASE).EQ.0)GOTO 9282
      IF(OBJ.NE.MIRROR.OR..NOT.CLOSED)GOTO 2011 
      CALL RSPEAK(197)
      GOTO 19000
  
 9282 SPK=198 
      IF(TOTING(VASE))CALL DROP(VASE,LOC) 
      PROP(VASE)=2
      FIXED(VASE)=-1
      GOTO 2011 
  
***WAKE.  ONLY USE IS TO DISTURB THE DWARVES. 
  
 9290 IF(OBJ.NE.DWARF.OR..NOT.CLOSED)GOTO 2011
      CALL RSPEAK(199)
      GOTO 19000
  
***SUSPEND. 
  
8300  WRITE (6,8302)
8302  FORMAT(//,' Saving game...',//)
      call svcomn (.FALSE.,' ')
!!!!! STOP
      call sys$exit(%val(1))

***WEAR. ONLY WORKS FOR RING AND MAIL...   AND FOOD!
  
 9310 IF(OBJ.NE.RING.AND.OBJ.NE.MAIL) GOTO 9315 
      IF(OBJ.EQ.RING) GOTO 9314 
      IF(OBJ.EQ.MAIL.AND.WEARING(2)) GOTO 9312
      WEARING(2)=.TRUE. 
      CALL CARRY(OBJ,LOC) 
      PROP(MAIL)=1
      CALL PSPEAK(MAIL,PROP(MAIL))
      GOTO 2012 
 9312 WRITE(6,9313) 
 9313 FORMAT(' You are already wearing it!')
      GOTO 2012 
 9314 IF(WEARING(1)) GOTO 9312
      WEARING(1)=.TRUE. 
      CALL CARRY(OBJ,LOC) 
      PROP(RING)=1
      CALL PSPEAK(RING,PROP(RING))
      GOTO 2012 
 9315 IF(OBJ.EQ.FOOD) GOTO 9318 
      WRITE(6,9316) 
 9316 FORMAT(' You can''t wear that!') 
      GOTO 2012 
 9318 WRITE(6,9319) 
 9319 FORMAT(' Ugh-- What a mess!') 
      CALL MOVE(FOOD,0) 
      GOTO 2012 
 8310 WRITE(6,8318) 
 8318 FORMAT(/' What are you trying to wear?')
      GOTO 2600 
  
***PISS.  AHHHH.... ... .. . . RELIEF.
  
***HE CANNOT PISS AN OBJECT, OR HAVE AN OBJECT PISS.
  
 9320 SPK=223 
      GOTO 2011 
  
 8320 IF(BFULL) GOTO 8325 
      SPK=222 
      GOTO 2011 
  
 8325 BFULL=.FALSE. 
      WAITNG=9999999
      OOPS=  9999999
      CALL RSPEAK(210)
      IF(HERE(PLANT).OR.HERE(PLANT2)) GOTO 9131 
      GOTO 2012 
  
***SWIM.  HE CAN ONLY SWIM IN THE RIVER, IF HE CAN FIND IT! 
  
 9330 IF(OBJ.EQ.FOUNTN.OR.OBJ.EQ.WATER) GOTO 8330 
      GOTO 2011 
 8330 IF(LIQLOC(LOC).NE.WATER) GOTO 2011
      IF ( .NOT. TOTING(NUGGET) ) GOTO 9331 
      CALL RSPEAK (228) 
      GOTO 99 
 9331 CONTINUE
      TEM=LOC-PLAC(BOAT)+2
      IF(TEM.LT.1.OR.TEM.GT.3) GOTO 8332
      NEWLOC=LOC+1
 8331 OLDLOC=LOC
      OLDLC2=OLDLOC 
      CALL RSPEAK(227)
      GOTO 2
 8332 IF(TEM.NE.5) GOTO 8334
      NEWLOC=PLAC(RUNES)
      GOTO 8331 
 8334 IF(LOC.NE.PLAC(RUNES)) GOTO 8336
      NEWLOC=RINGLOC
      GOTO 8331 
 8336 IF(LOC.NE.RINGLOC) GOTO 8338
***   SPK=YOU ARE NOW WET...
      SPK=70
      IF(PLACE(RING).NE.0) GOTO 2011
***   SPK=YOU HAVE JUMPED INTO...   (AND FOUND THE RING!) 
      CALL MOVE(RING,RINGLOC) 
      SPK=219 
      GOTO 2011 
 8338 CONTINUE
***   SPK=YOU NOW ARE WET...
      SPK=70
      GOTO 2011 

*     Restore.

 8400 WRITE (6,8402)
 8402 FORMAT(//,' Restoring game...',//)
      call ldcomn (.FALSE.,FLNAME)
      goto 2606  


***                      *******************
*                        ***** OBJECTS *****
*                        *******************
*  ANALYSE AN OBJECT WORD.  SEE IF THE THING IS HERE, WHETHER WE'VE GOT 
*  YET, AND SO ON.  OBJECT MUST BE HERE UNLESS VERB IS 'FIND' OR 'INVENT
*  (AND NO NEW VERB YET TO BE ANALYSED).  WATER AND OIL ARE ALSO FUNNY, 
*  THEY ARE NEVER ACTUALLY DROPPED AT ANY LOCATION, BUT MIGHT BE HERE IN
*  THE BOTTLE OR AS A FEATURE OF THE LOCATION.
  
 5000 OBJ=K 
      IF(WIZKID) GOTO 5010
      IF(FIXED(OBJ).NE.LOC.AND..NOT.HERE(OBJ))GOTO 5100 
      IF(DARK(0)) GOTO 5100 
 5010 IF(WD2.NE.' ')GOTO 2800 
      IF(OBJ.EQ.SWITCH) GOTO 5150 
      IF(VERB.NE.0)GOTO 4090
      WRITE(6,5015) WD1
 5015 FORMAT(/,' What do you want to do with the ',A10,'?')
      GOTO 2600 
  
 5100 IF(OBJ.NE.GRATE)GOTO 5110 
      IF(LOC.EQ.1.OR.LOC.EQ.4.OR.LOC.EQ.7)OBJ=DPRSSN
      IF(LOC.GT.9.AND.LOC.LT.15)K=ENTRNC
      IF(OBJ.NE.GRATE)GOTO 3000 
 5110 IF(OBJ.NE.DWARF)GOTO 5120 
      DO 5112 LOOP=1,7
      IF(DLOC(LOOP).EQ.LOC.AND.DFLAG.GE.2)GOTO 5010 
 5112 CONTINUE
 5120 IF((LIQ(0).EQ.OBJ.AND.HERE(BOTTLE)).OR.OBJ.EQ.LIQLOC(LOC))
     $GOTO 5010 
      IF(OBJ.NE.PLANT.OR..NOT.AT(PLANT2).OR.PROP(PLANT2).EQ.0)GOTO 5130 
      OBJ=PLANT2
      GOTO 5010 
 5130 IF(OBJ.NE.KNIFE.OR.KNFLOC.NE.LOC)GOTO 5140
      KNFLOC=-1 
      SPK=116 
      GOTO 2011 
 5140 IF(OBJ.NE.ROD.OR..NOT.HERE(ROD2))GOTO 5190
      OBJ=ROD2
      GOTO 5010 
  
***FLIP SWITCH. THIS WILL LIGHT UP THE WHOLE CAVE AND UP THE MOVE LIMIT 
*  TO 600..... OR IT WILL KILL YOU. 
  
 5150 IF(LIT.AND.VERB.NE.ON) GOTO 5152
      IF(VERB.EQ.OFF) GOTO 5152 
      IF(WIZKID) GOTO 5151
      IF(HERE(SWITCH).AND.WEARING(1)) GOTO 5151 
      CALL RSPEAK(211)
  
***OH-OH! HE DIED.
  
      GOTO 99 
 5151 LIT=.TRUE.
      LIMIT=600 
      CALL RSPEAK(212)
      GOTO 2
 5152 LIT=.FALSE. 
      SPK=213 
      GOTO 2011 
  
 5190 IF((VERB.EQ.FIND.OR.VERB.EQ.INVENT).AND.WD2.EQ.' ')GOTO 5010
      IF(WIZKID.AND.PLAC(OBJ).NE.0.AND.PLACE(OBJ).EQ.0) GOTO 5010 
      WRITE(6,5199) WD1 
 5199 FORMAT(/,' I see no ',A10,' here.') 
      GOTO 2012 
  
***                      *******************
*                        ****** HINTS ******
*                        *******************
* 
*  COME HERE IF HE'S BEEN LONG ENOUGH AT REQUIRED LOC(S) FOR SOME UNUSED
*  HINT NUMBER IS IN VARIABLE 'HINT'.  BRANCH TO QUICK TEST FOR ADDITION
*  CONDITIONS, THEN COME BACK TO DO NEAT STUFF.  GOTO 40010 IF CONDITION
*  MET AND WE WANT TO OFFER THE HINT.  GOTO 40020 TO CLEAR HINTLC BACK T
*  40030 TO TAKE NO ACTION YET. 
  
40000 HINTM3=HINT-3 
      GOTO (40400,40500,40600,40700,40800,40900),HINTM3 
*           CAVE  MONGOOS  SNAKE MAZE  DARK  WITT 
      CALL BUG(27)
  
40010 HINTLC(HINT)=0
      IF(.NOT.YES(HINTS(HINT,3),0,54,WCOUNT))GOTO 2602
      WRITE(6,40012)HINTS(HINT,2) 
40012 FORMAT(/,' I am prepared to give you a hint, but it will cost you'
     $,I2,' points.') 
      HINTED(HINT)=YES(175,HINTS(HINT,4),54,WCOUNT) 
      IF(HINTED(HINT).AND.LIMIT.GT.30)LIMIT=LIMIT+30*HINTS(HINT,2)
40020 HINTLC(HINT)=0
40030 GOTO 2602 
  
***NOW FOR THE QUICK TESTS.  SEE DATABASE DESCRIPTION FOR ONE-LINE NOTES
  
40400 IF(PROP(GRATE).EQ.0.AND..NOT.HERE(KEYS))GOTO 40010
      GOTO 40020
  
40500 IF(LOOSE.AND.OBJ.EQ.MONGOOS)GOTO 40010
      GOTO 40030
  
40600 IF(HERE(SNAKE).AND..NOT.HERE(MONGOOS))GOTO 40010
      GOTO 40020
  
40700 IF(ATLOC(LOC).EQ.0.AND.ATLOC(OLDLOC).EQ.0 
     $.AND.ATLOC(OLDLC2).EQ.0.AND.HOLDNG.GT.1)GOTO 40010
      GOTO 40020
  
40800 IF(PROP(EMRALD).NE.-1.AND.PROP(PYRAM).EQ.-1)GOTO 40010
      GOTO 40020
  
40900 GOTO 40010
*** 
*  CAVE CLOSING AND SCORING 
  
  
***THESE SECTIONS HANDLE THE CLOSING OF THE CAVE.  THE CAVE CLOSES 'CLOC
*  TURNS AFTER THE LAST TREASURE HAS BEEN LOCATED (INCLUDING THE PIRATE'
*  CHEST, WHICH MAY OF COURSE NEVER SHOW UP).  NOTE THAT THE TREASURES N
*  HAVE BEEN TAKEN YET, JUST LOCATED.  HENCE CLOCK1 MUST BE LARGE ENOUGH
*  OUT OF THE CAVE (IT ONLY TICKS WHILE INSIDE THE CAVE).  WHEN IT HITS 
*  WE BRANCH TO 10000 TO START CLOSING THE CAVE, AND THEN SIT BACK AND W
*  HIM TO TRY TO GET OUT.  IF HE DOESN'T WITHIN CLOCK2 TURNS, WE CLOSE T
*  CAVE; IF HE DOES TRY, WE ASSUME HE PANICS, AND GIVE HIM A FEW ADDITIO
*  TURNS TO GET FRANTIC BEFORE WE CLOSE.  WHEN CLOCK2 HITS ZERO, WE BRAN
*  11000 TO TRANSPORT HIM INTO THE FINAL PUZZLE.  NOTE THAT THE PUZZLE D
*  UPON ALL SORTS OF RANDOM THINGS.  FOR INSTANCE, THERE MUST BE NO WATE
*  OIL, SINCE THERE ARE BEANSTALKS WHICH WE DON'T WANT TO BE ABLE TO WAT
*  SINCE THE CODE CAN'T HANDLE IT.  ALSO, WE CAN HAVE NO KEYS, SINCE THE
*  GRATE (HAVING MOVED THE FIXED OBJECT!) THERE SEPARATING HIM FROM ALL 
*  TREASURES.  MOST OF THESE PROBLEMS ARISE FROM THE USE OF NEGATIVE PRO
*  NUMBERS TO SUPPRESS THE OBJECT DESCRIPTIONS UNTIL HE'S ACTUALLY MOVED
*  OBJECTS. 
  
*  WHEN THE FIRST WARNING COMES, WE LOCK THE GRATE, DESTROY THE BRIDGE, 
*  ALL THE DWARVES (AND THE PIRATE), REMOVE THE TROLL AND BEAR (UNLESS D
*  AND SET 'CLOSNG' TO TRUE.  LEAVE THE DRAGON; TOO MUCH TROUBLE TO MOVE
*  FROM NOW UNTIL CLOCK2 RUNS OUT, HE CANNOT UNLOCK THE GRATE, MOVE TO A
*  LOCATION OUTSIDE THE CAVE (LOC<9), OR CREATE THE BRIDGE.  NOR CAN HE 
*  RESURRECTED IF HE DIES.  NOTE THAT THE SNAKE IS ALREADY GONE, SINCE H
*  TO THE TREASURE ACCESSIBLE ONLY VIA THE HALL OF THE MT. KING.  ALSO, 
*  BEEN IN GIANT ROOM (TO GET EGGS), SO WE CAN REFER TO IT.  ALSO ALSO, 
*  GOTTEN THE PEARL, SO WE KNOW THE BIVALVE IS AN OYSTER.  *AND*, THE DW
*  MUST HAVE BEEN ACTIVATED, SINCE WE'VE FOUND CHEST. 
  
10000 PROP(GRATE)=0 
      PROP(FISSUR)=0
      DO 10010 LOOP=1,8 
      DSEEN(LOOP)=.FALSE. 
10010 DLOC(LOOP)=0
      CALL MOVE(TROLL,0)
      CALL MOVE(TROLL+100,0)
      CALL MOVE(TROLL2,PLAC(TROLL)) 
      CALL MOVE(TROLL2+100,FIXD(TROLL)) 
      CALL JUGGLE(CHASM)
      CALL MOVE(BEAR,0) 
      PROP(CHAIN)=0 
      FIXED(CHAIN)=0
      PROP(AXE)=0 
      FIXED(AXE)=0
      CALL RSPEAK(129)
      CLOCK1=-1 
      CLOSNG=.TRUE. 
      GOTO 19999
  
***ONCE HE'S PANICKED, AND CLOCK2 HAS RUN OUT, WE COME HERE TO SET UP TH
*  STORAGE ROOM.  THE ROOM HAS TWO LOCS, HARDWIRED AS 115 (NE) AND 116 (
*  AT THE NE END, WE PLACE EMPTY BOTTLES, A NURSERY OF PLANTS, A BED OF 
*  OYSTERS, A PILE OF LAMPS, RODS WITH STARS, SLEEPING DWARVES, AND HIM.
*  THE SW END WE PLACE GRATE OVER TREASURES, SNAKE PIT, COVEY OF SACKED B 
*  MORE RODS, AND PILLOWS.  A MIRROR STRETCHES ACROSS ONE WALL.  MANY OF
*  OBJECTS COME FROM KNOWN LOCATIONS AND/OR STATES (E.G. THE SNAKE IS KN
*  HAVE BEEN DESTROYED AND NEEDN'T BE CARRIED AWAY FROM ITS OLD 'PLACE')
*  MAKING THE VARIOUS OBJECTS BE HANDLED DIFFERENTLY.  WE ALSO DROP ALL 
*  OBJECTS HE MIGHT BE CARRYING (LEST HE HAVE SOME WHICH COULD CAUSE TRO
*  SUCH AS THE KEYS).  WE DESCRIBE THE FLASH OF LIGHT AND TRUNDLE BACK. 
  
11000 PROP(BOTTLE)=PUT(BOTTLE,115,1)
      PROP(PLANT)=PUT(PLANT,115,0)
      PROP(OYSTER)=PUT(OYSTER,115,0)
      PROP(LAMP)=PUT(LAMP,115,0)
      PROP(ROD)=PUT(ROD,115,0)
      PROP(DWARF)=PUT(DWARF,115,0)
      LOC=115 
      OLDLOC=115
      NEWLOC=115
  
*  LEAVE THE GRATE WITH NORMAL (NON-NEGATIVE PROPERTY). 
  
      PROP(GRATE)=PUT(GRATE,116,-1) 
      PROP(SNAKE)=PUT(SNAKE,116,1)
      PROP(MONGOOS)=PUT(MONGOOS,116,1)
      PROP(SACK)=PUT(SACK,116,0)
      PROP(ROD2)=PUT(ROD2,116,0)
      PROP(PILLOW)=PUT(PILLOW,116,0)
      PROP(MIRROR)=PUT(MIRROR,115,0)
      FIXED(MIRROR)=116 
  
*  DISTROY THE OBJECTS THAT HE IS TOTING. 
  
      DO 11010 LOOP=1,100 
11010 IF(TOTING(LOOP))CALL MOVE(LOOP,0) 
  
      CALL RSPEAK(132)
      CLOSED=.TRUE. 
      GOTO 2
  
***ANOTHER WAY WE CAN FORCE AN END TO THINGS IS BY HAVING THE LAMP GIVE 
*  WHEN IT GETS CLOSE, WE COME HERE TO WARN HIM.  WE GO TO 12000 IF THE 
*  AND FRESH BATTERIES ARE HERE, IN WHICH CASE WE REPLACE THE BATTERIES 
*  CONTINUE.  12200 IS FOR OTHER CASES OF LAMP DYING.  12400 IS WHEN IT 
*  OUT, AND 12600 IS IF HE'S WANDERED OUTSIDE AND THE LAMP IS USED UP, I
*  CASE WE FORCE HIM TO GIVE UP.
  
12000 CALL RSPEAK(188)
      PROP(BATTER)=1
      IF(TOTING(BATTER))CALL DROP(BATTER,LOC) 
      LIMIT=LIMIT+2500
      LMWARN=.FALSE.
      GOTO 19999
  
12200 IF(LMWARN.OR..NOT.HERE(LAMP))GOTO 19999 
      LMWARN=.TRUE. 
      SPK=187 
      IF(PLACE(BATTER).EQ.0)SPK=183 
      IF(PROP(BATTER).EQ.1)SPK=189
      CALL RSPEAK(SPK)
      GOTO 19999
  
12400 LIMIT=-1
      PROP(LAMP)=0
      IF(HERE(LAMP))CALL RSPEAK(184)
      GOTO 19999
  
12600 CALL RSPEAK(185)
      GAVEUP=.TRUE. 
      GOTO 20000
  
***OH DEAR, HE'S DISTURBED THE DWARVES. 
  
19000 CALL RSPEAK(136)
  
***EXIT CODE.  WILL EVENTUALLY INCLUDE SCORING.  FOR NOW, HOWEVER, ...
  
*  THE PRESENT SCORING ALGORITHM IS AS FOLLOWS: 
*     OBJECTIVE:          POINTS:        PRESENT TOTAL POSSIBLE:  
*  EACH TREASURE < CHEST    12                    60
*  TREASURE CHEST ITSELF    14                    14
*  EACH TREASURE > CHEST    16                   192
*  DYING                   -10                     0
*  GETTING WELL INTO CAVE   25                    25
*  NOT QUITTING              4                     4
*  REACHING 'CLOSNG'        25                    25
*  'CLOSED': QUIT/KILLED    10                    10
*            KLUTZED        25                    25
*            WRONG WAY      30                    30
*            SUCCESS        45                    45
*  CAME TO WITT'S END        2                     2
*  ROUND OUT THE TOTAL       3                     3
*                                       TOTAL:   360
*  (POINTS CAN ALSO BE DEDUCTED FOR USING HINTS.) 
  
20000 SCORE=0 
      MXSCOR=0
  
***FIRST TALLY UP THE TREASURES.  MUST BE IN BUILDING AND NOT BROKEN. 
*  GIVE THE POOR GUY 2 POINTS JUST FOR FINDING EACH TREASURE. 
  
      DO 20010 LOOP=TRSURE,MAXTRS 
      IF(PTEXT(LOOP).EQ.0)GOTO 20010
      K=12
      IF(LOOP.EQ.CHEST)K=14 
      IF(LOOP.GT.CHEST)K=16 
      IF(PROP(LOOP).GE.0)SCORE=SCORE+2
      IF(PLACE(LOOP).EQ.3.AND.PROP(LOOP).EQ.0)SCORE=SCORE+K-2 
      MXSCOR=MXSCOR+K 
20010 CONTINUE
  
***NOW LOOK AT HOW HE FINISHED AND HOW FAR HE GOT.  MAXDIE AND NUMDIE TE
*  HOW WELL HE SURVIVED.  GAVEUP SAYS WHETHER HE EXITED VIA QUIT.  DFLAG
*  TELL US IF HE EVER GOT SUITABLY DEEP INTO THE CAVE.  CLOSNG STILL IND
*  WHETHER HE REACHED THE ENDGAME.  AND IF HE GOT AS FAR AS 'CAVE CLOSED
*  (INDICATED BY 'CLOSED'), THEN BONUS IS ZERO FOR MUNDANE EXITS OR 133,
*  135 IF HE BLEW IT (SO TO SPEAK). 
  
      SCORE=SCORE-NUMDIE*10 
      IF(DFLAG.NE.0)SCORE=SCORE+25
      MXSCOR=MXSCOR+25
      IF(.NOT.(SCORNG.OR.GAVEUP))SCORE=SCORE+4
      MXSCOR=MXSCOR+4 
      IF(CLOSNG)SCORE=SCORE+25
      MXSCOR=MXSCOR+25
      IF(.NOT.CLOSED)GOTO 20020 
      IF(BONUS.EQ.0)SCORE=SCORE+10
      IF(BONUS.EQ.135)SCORE=SCORE+25
      IF(BONUS.EQ.134)SCORE=SCORE+30
      IF(BONUS.EQ.133)SCORE=SCORE+45
20020 MXSCOR=MXSCOR+45
  
***DID HE COME TO WITT'S END AS HE SHOULD?
  
      IF(PLACE(MAGZIN).EQ.108)SCORE=SCORE+2 
      MXSCOR=MXSCOR+2 
  
***ROUND IT OFF.
  
      SCORE=SCORE+3 
      MXSCOR=MXSCOR+3 
  
***DEDUCT POINTS FOR HINTS.  HINTS < 4 ARE SPECIAL; SEE DATABASE DESCRIP
  
      DO 20030 LOOP=1,HNTMAX
20030 IF(HINTED(LOOP))SCORE=SCORE-HINTS(LOOP,2) 
  
***RETURN TO SCORE COMMAND IF THAT'S WHERE WE CAME FROM.
  
      IF(SCORNG)GOTO 8241 
  
* 
***THAT SHOULD BE GOOD ENOUGH.  LET'S TELL HIM ALL ABOUT IT.
  
      WRITE(6,20100)SCORE,MXSCOR,TURNS
20100 FORMAT(///,' You scored',I4,' out of a possible',I4,
     $', using',I5,' turns.') 
  
      IF(WIZKID) GOTO 25000 
      DO 20200 LOOP=1,CLSSES
      I=LOOP
      IF(CVAL(LOOP).GE.SCORE)GOTO 20210 
20200 CONTINUE
      WRITE(6,20202)
20202 FORMAT(/,' You just went off my scale!!',/) 
      GOTO 25000
  
20210 CALL SPEAK(CTEXT(I))
      IF(I.EQ.CLSSES-1)GOTO 20220 
      K=CVAL(I)+1-SCORE 
      KK='s.' 
      IF(K.EQ.1)KK='. ' 
      WRITE(6,20212)K,KK
20212 FORMAT(/,' To achieve the next higher rating, you need',I3, 
     $' more point',A2/)
      GOTO 25000
  
20220 WRITE(6,20222)
20222 FORMAT(/,' To achieve the next higher rating ', 
     $'would be a neat trick!',//,' Congratulations!!',/) 
  
25000 CONTINUE
!!!!! STOP
      call sys$exit(%val(1))
      END 
